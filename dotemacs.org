#+AUTHOR: 张俊(geekard@qq.com)
#+LASTMOD: 2023-02-20T12:02:16+0800
#+STARTUP: overview nohideblocks
#+PROPERTY: header-args:emacs-lisp :tangle yes :results silent :exports code :eval no

* install

编译安装 Emacs 29:
#+begin_src bash :tangle ~/.emacs.d/init.sh
# brew uninstall emacs-plus@29
brew install emacs-plus@29  --with-no-frame-refocus --with-xwidgets --with-imagemagick --with-poll --with-dragon-icon # --with-native-comp
brew link --overwrite emacs-plus@29
ln -sf /usr/local/opt/emacs-plus@29/Emacs.app /Applications
#+end_src

* init

=early-init.el= 是 =Emacs= 启动时最开始执行的文件，执行复杂逻辑可能导致启动失败，所以该文件尽量以变量定义为主。
#+begin_src emacs-lisp :tangle ~/.emacs.d/early-init.el
(when (fboundp 'native-compile-async)
  (setenv "LIBRARY_PATH"
          (concat (getenv "LIBRARY_PATH") "/usr/local/opt/gcc/lib/gcc/12:/usr/local/opt/gcc/lib/gcc/12/gcc/x86_64-apple-darwin21/12"))
  (setq native-comp-speed 2)
  (setq native-comp-async-jobs-number 4)
  ;; Emacs 29;
  ;;(setq inhibit-automatic-native-compilation t)
  ;;(setq native-comp-async-report-warnings-errors 'silent)
  )

;; 加载较新的 .el 文件。
(setq-default load-prefer-newer t)
(setq-default lexical-binding t)
(setq lexical-binding t)

;; 关闭 cl 告警。
(require 'cl-lib)
(setq byte-compile-warnings '(cl-functions))

;; 关闭 package.el(后续使用 straight.el) 。
(setq package-enable-at-startup nil)

;; 在单独文件保存自定义配置，避免污染 ~/.emacs 文件。
(setq custom-file (expand-file-name "~/.emacs.d/custom.el"))
(add-hook 'after-init-hook (lambda () (when (file-exists-p custom-file) (load custom-file))))

;; 个人信息。
(setq user-full-name "zhangjun")
(setq user-mail-address "geekard@qq.com")
#+end_src

* package

使用 straight.el 替换 Emacs 内置的 package.el, 它用 =Git Checkout+Build= 机制安装软件包（而非直接从软件源下载）：
+ use-package 可以和 straight.el 和 package.el 结合使用。
+ M-x use-package-report :: 查看 package 加载时间（按 S 排序）。
#+begin_src emacs-lisp
;; 配置 use-package 使用 straight.el 安装包。
(setq straight-use-package-by-default t)
;; 只 clone 最近一次 commit 历史, 减少磁盘空间占用。
(setq straight-vc-git-default-clone-depth 1)
(setq straight-check-for-modifications '(check-on-save find-when-checking watch-files))
(setq straight-host-usernames '((github . "opsnull")))

;; 安装 straight.el。
(defvar bootstrap-version)
(let ((bootstrap-file (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 5))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(setq use-package-verbose t)
(setq use-package-always-demand t)
(setq use-package-compute-statistics t)
;; 安装 use-package。
(straight-use-package 'use-package)
#+end_src

=exec-path-from-shell= 将 =Shell= 环境变量拷贝到 =Emacs= ：
#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :custom
  ;; 去掉 -l 参数, 加快启动速度。
  (exec-path-from-shell-arguments '("-l")) 
  (exec-path-from-shell-check-startup-files nil)
  (exec-path-from-shell-variables '("PATH" "MANPATH" "GOPATH" "GOPROXY" "GOPRIVATE" "GOFLAGS" "GO111MODULE" "PYTHONPATH"))
  :config
  (when (memq window-system '(mac ns x))
    (exec-path-from-shell-initialize)))
#+end_src

* tuning

性能调优: 参考 [[https://github.com/hlissner/doom-emacs/blob/develop/core/core.el][doom core.el]]
#+begin_src emacs-lisp
;; 提升 IO 性能。
(setq process-adaptive-read-buffering nil)
;; 增加单次读取进程输出的数据量（缺省 4KB) 。
(setq read-process-output-max (* 1024 1024 10))

;; Garbage Collector Magic Hack
(use-package gcmh
  :init
  ;; 在 minibuffer 显示 GC 信息。
  ;;(setq garbage-collection-messages t)
  ;;(setq gcmh-verbose t)
  (setq gcmh-idle-delay 5)
  (setq gcmh-high-cons-threshold (* 100 1024 1024))
  (gcmh-mode 1)
  (gcmh-set-high-threshold))

;; 缺省使用 email 地址加密。
(setq-default epa-file-select-keys nil)
(setq-default epa-file-encrypt-to user-mail-address)
;; 使用 minibuffer 输入 GPG 密码。
(setq-default epa-pinentry-mode 'loopback)
;; 认证信息文件。
(setq auth-sources '("~/.authinfo.gpg" "~/work/proxylist/hosts_auth"))
;; 缓存对称加密密码。
(setq epa-file-cache-passphrase-for-symmetric-encryption t)
;; gpg 文件。
(require 'epa-file)
(epa-file-enable)
;; 认证不过期, 默认 7200。
(setq auth-source-cache-expiry nil)
;;(setq auth-source-debug t)

;; 关闭容易误操作的按键。
(global-unset-key (kbd "s-w"))
(global-unset-key (kbd "C-z"))
(global-unset-key (kbd "<mouse-2>"))
(global-unset-key (kbd "s-k"))
(global-unset-key (kbd "s-o"))
(global-unset-key (kbd "s-t"))
(global-unset-key (kbd "s-p"))
(global-unset-key (kbd "s-n"))
(global-unset-key (kbd "s-,"))
(global-unset-key (kbd "s-."))
(global-unset-key (kbd "C-<wheel-down>"))
(global-unset-key (kbd "C-<wheel-up>"))
#+end_src

* face
** ui

#+begin_src emacs-lisp
(when (memq window-system '(mac ns x))
  (tool-bar-mode -1)
  (scroll-bar-mode -1)
  (menu-bar-mode -1)
  (setq use-file-dialog nil)
  (setq use-dialog-box nil))

;; 关闭启动消息。
(setq inhibit-startup-screen t)
(setq inhibit-startup-message t)
(setq inhibit-startup-echo-area-message t)
(setq initial-scratch-message nil)

;; 指针闪动。
(blink-cursor-mode t)
;; 光标和字符宽度一致（如 TAB)
(setq x-stretch-cursor nil)

;; 不显示 window fringe, 显示多个 window 时更紧凑。
(set-fringe-style 0)

;; 出错提示。
(setq visible-bell t)

;; 窗口间显示分割线。
;;(setq window-divider-default-places t)
;;(add-hook 'window-setup-hook #'window-divider-mode)

;; 30: 左右分屏, nil: 上下分屏。
(setq split-width-threshold 30)
;; 滚动一屏后显示 3 行上下文。
(setq next-screen-context-lines 3)

;; 高亮当前行。
(setq global-hl-line-sticky-flag t)
(global-hl-line-mode t)

;; 平滑地进行半屏滚动(默认避免滚动后进行 recenter)。
(setq scroll-step 1)
(setq scroll-conservatively 10000)
(setq scroll-margin 2)
;; 像素平滑滚动（Emacs 29 开始支持）。
(if (boundp 'pixel-scroll-precision-mode)
    (pixel-scroll-precision-mode t))

;; 大文件不显示行号。
(setq large-file-warning-threshold nil)
(setq line-number-display-limit large-file-warning-threshold)
(setq line-number-display-limit-width 1000)

;; 显示行号。
(dolist (mode '(text-mode-hook prog-mode-hook conf-mode-hook))
  (add-hook mode (lambda () (display-line-numbers-mode 1))))

;; Emacs 29: No Titlebar
(add-to-list 'default-frame-alist '(undecorated-round . t))

;; 加 t 参数让 togg-frame-XX 最后运行，这样最大化才生效。
;;(add-hook 'window-setup-hook 'toggle-frame-fullscreen t) 
(add-hook 'window-setup-hook 'toggle-frame-maximized t)

;; 不在新 frame 打开文件（如 Finder 的 "Open with Emacs") 。
(setq ns-pop-up-frames nil)
;; 复用当前 frame。
(setq display-buffer-reuse-frames t)
(setq display-buffer-base-action
      '(display-buffer-reuse-mode-window
        display-buffer-reuse-window
        display-buffer-same-window))
;; If a popup does happen, don't resize windows to be equal-sized
(setq even-window-sizes nil)

;; 手动刷行显示.
(global-set-key (kbd "<f5>") #'redraw-display)

;; 在 frame 底部显示窗口。
(setq display-buffer-alist
      `((,(rx bos (or
                   "*Apropos*"
                   "*Help*"
                   "*helpful"
                   "*info*"
                   "*Summary*"
                   "*vterm"
                   "*lsp-bridge"
                   "*Org"
                   "*Google Translate*"
                   "Shell Command Output") (0+ not-newline))
         (display-buffer-reuse-mode-window display-buffer-below-selected)
         (window-height . 0.33)
         (mode apropos-mode help-mode helpful-mode Info-mode Man-mode))))

;; 透明背景。
(defun my/toggle-transparency ()
  (interactive)
  (set-frame-parameter (selected-frame) 'alpha '(90 . 90))
  (add-to-list 'default-frame-alist '(alpha . (90 . 90))))
#+end_src
+ 不开启 =variable-pitch= face 和 mode, 否则 doom-modeline 右侧容易溢出。

** dashboard

#+begin_src emacs-lisp
(use-package dashboard
  :config
  (dashboard-setup-startup-hook)
  (setq-local global-hl-line-mode nil)
  (setq dashboard-banner-logo-title "Happy Hacking & Writing 🎯")
  (setq dashboard-projects-backend #'project-el)
  (setq dashboard-center-content t)
  (setq dashboard-set-heading-icons t)
  (setq dashboard-set-navigator t)
  (setq dashboard-set-file-icons t)
  (setq dashboard-items '((recents . 15) (projects . 8) (agenda . 3)))) 
#+end_src

** doom-modeline

#+begin_src emacs-lisp
(use-package doom-modeline
  :hook (after-init . doom-modeline-mode)
  :custom
  ;; 不显示换行和编码。
  (doom-modeline-buffer-encoding nil)
  ;; 显示语言版本。
  (doom-modeline-env-version t)
  ;; 不显示 Go 版本。
  (doom-modeline-env-enable-go nil)
  (doom-modeline-buffer-file-name-style 'truncate-nil) ;; relative-from-project
  (doom-modeline-vcs-max-length 30)
  (doom-modeline-github nil)
  (doom-modeline-height 1)
  (doom-modeline-time-icon nil)
  :config
  ;; 电池和日期。
  (display-battery-mode -1)
  (column-number-mode t)
  (size-indication-mode -1)
  (display-time-mode t)
  (setq display-time-24hr-format t)
  ;; system load 大于 10 时才在 modeline 显示；
  (setq display-time-default-load-average nil)
  (setq display-time-load-average-threshold 10)
  (setq display-time-format "%m/%d[%w]%H:%M")
  (setq display-time-day-and-date t)
  (setq indicate-buffer-boundaries (quote left)))
#+end_src

** ef-themes

#+begin_src emacs-lisp
(use-package ef-themes
  :straight (ef-themes :host github :repo "protesilaos/ef-themes")
  :config
  ;; Disable all other themes to avoid awkward blending:
  (mapc #'disable-theme custom-enabled-themes)
  ;; 关闭 variable-pitch 模式，否则 modeline 可能溢出。
  (setq ef-themes-mixed-fonts t
        ef-themes-variable-pitch-ui nil)
  (setq ef-themes-region '(intense no-extend neutral)))
#+end_src

跟随 Mac 自动切换深浅主题:
#+begin_src emacs-lisp
(defun my/load-light-theme () (interactive) (load-theme 'ef-day t)) ;; ef-day doom-one-light
(defun my/load-dark-theme () (interactive) (load-theme 'ef-night t)) ;; ef-night doom-palenight
(add-hook 'ns-system-appearance-change-functions
          (lambda (appearance)
            (pcase appearance
              ('light (my/load-light-theme))
              ('dark (my/load-dark-theme)))))
#+end_src

** tab-bar

#+begin_src emacs-lisp
(use-package tab-bar
  :straight (:type built-in)
  :custom
  (tab-bar-close-button-show nil)
  (tab-bar-history-limit 20)
  (tab-bar-new-tab-choice "*dashboard*")
  (tab-bar-show 1)
  (tab-bar-tab-hints t) ;; 显示 tab 序号。
  (tab-bar-select-tab-modifiers "super") ;; 使用 super + N 来切换 tab。
  :config
  (tab-bar-history-mode t)
  ;;(setq tab-bar-format '(tab-bar-format-tabs-groups))
  ;;(add-to-list 'tab-bar-format #'tab-bar-format-tabs-groups)
  (global-set-key (kbd "C-s-j") 'tab-bar-history-back)
  (global-set-key (kbd "C-s-k") 'tab-bar-history-forward)  
  (global-set-key (kbd "s-[") 'tab-bar-switch-to-prev-tab)
  (global-set-key (kbd "s-]") 'tab-bar-switch-to-next-tab)
  (global-set-key (kbd "s-t") 'tab-bar-new-tab)
  (global-set-key (kbd "s-w") 'tab-bar-close-tab)
  (global-set-key (kbd "s-1") 'tab-bar-select-tab)
  (global-set-key (kbd "s-2") 'tab-bar-select-tab)
  (global-set-key (kbd "s-3") 'tab-bar-select-tab)
  (global-set-key (kbd "s-4") 'tab-bar-select-tab)
  (global-set-key (kbd "s-5") 'tab-bar-select-tab)
  (global-set-key (kbd "s-6") 'tab-bar-select-tab)
  (global-set-key (kbd "s-7") 'tab-bar-select-tab)
  (global-set-key (kbd "s-8") 'tab-bar-select-tab)
  (global-set-key (kbd "s-9") 'tab-bar-select-tab))
#+end_src

** sort-tab
#+begin_src emacs-lisp
(use-package sort-tab
  :demand
  :straight (:repo "manateelazycat/sort-tab" :host github)
  ;; emacs 启动后再启用 sort-tab 防止显示异常。
  :hook (after-init . sort-tab-mode)
  :config
  ;;(sort-tab-mode 1)
  (setq sort-tab-show-index-number t)
  (setq sort-tab-height 40)
  (global-set-key (kbd "s-n") 'sort-tab-select-next-tab)
  (global-set-key (kbd "s-p") 'sort-tab-select-prev-tab)
  (global-set-key (kbd "s-0") 'sort-tab-select-visible-tab)
  (global-set-key (kbd "s-Q") 'sort-tab-close-all-tabs)
  (global-set-key (kbd "s-q") 'sort-tab-close-mode-tabs)
  (global-set-key (kbd "s-;") 'sort-tab-close-current-tab)
  ;; 设置 tab 颜色，M-x list-colors-display。
  (set-face-foreground 'sort-tab-current-tab-face "peru")
  ;; 不显示背景颜色。
  (set-face-background 'sort-tab-current-tab-face nil))
#+end_src

* font

+ 中文（显示）：更纱黑体 Sarasa Mono SC: https://github.com/be5invis/Sarasa-Gothic
+ 英文（显示）：Fira Code : https://github.com/tonsky/FiraCode/wiki/Installing
+ 中英文（PDF) ：Noto CJK SC: https://github.com/googlefonts/noto-cjk.git
+ Symbols 字体:  Noto Sans Symbols 和 Noto Sans Symbols2: https://fonts.google.com/noto
+ 花園明朝：HanaMinB：http://fonts.jp/hanazono/
+ Emacs 默认后备字体：Symbola: https://dn-works.com/ufas/
#+begin_src emacs-lisp
;; 缺省字体（英文，如显示代码）。
(setq +font-family "Fira Code Retina")
(setq +modeline-font-family "Fira Code Retina")
;; 其它均使用 Sarasa Mono SC 字体。
(setq +fixed-pitch-family "Sarasa Mono SC")
(setq +variable-pitch-family "Sarasa Mono SC")
(setq +font-unicode-family "Sarasa Mono SC")
(setq +font-size 13)

;; 设置缺省字体。
(defun +load-base-font ()
  ;; 只为缺省字体设置 size, 其它字体都通过 :height 动态伸缩。
  (let* ((font-spec (format "%s-%d" +font-family +font-size)))
	(set-frame-parameter nil 'font font-spec)
	(add-to-list 'default-frame-alist `(font . ,font-spec))))

;; 设置各特定 face 的字体。
(defun +load-face-font (&optional frame)
  (let ((font-spec (format "%s" +font-family))
	    (modeline-font-spec (format "%s" +modeline-font-family))
	    (variable-pitch-font-spec (format "%s" +variable-pitch-family))
	    (fixed-pitch-font-spec (format "%s" +fixed-pitch-family)))
	(set-face-attribute 'variable-pitch frame :font variable-pitch-font-spec :height 1.0)
	(set-face-attribute 'fixed-pitch frame :font fixed-pitch-font-spec :height 1.1)
	(set-face-attribute 'fixed-pitch-serif frame :font fixed-pitch-font-spec :height 1.0)
	(set-face-attribute 'tab-bar frame :font font-spec :height 1.0)
	(set-face-attribute 'mode-line frame :font modeline-font-spec :height 1.0)
	(set-face-attribute 'mode-line-inactive frame :font modeline-font-spec :height 1.0)))

;; 设置中文字体。
(defun +load-ext-font ()
  (when window-system
	(let ((font (frame-parameter nil 'font))
		  (font-spec (font-spec :family +font-unicode-family)))
	  (dolist (charset '(kana han hangul cjk-misc bopomofo symbol))
	    (set-fontset-font font charset font-spec)))))

;; 设置 Emoji 字体。
(defun +load-emoji-font ()
  (when window-system
	(setq use-default-font-for-symbols nil)
	(set-fontset-font t '(#x1f000 . #x1faff) (font-spec :family "Apple Color Emoji"))
	(set-fontset-font t 'symbol (font-spec :family "Symbola"))))

(add-hook 'after-make-frame-functions 
		  ( lambda (f) 
		    (+load-face-font f)
		    (+load-ext-font)
		    (+load-emoji-font)))

;; 加载字体。
(defun +load-font ()
  (+load-base-font)
  (+load-face-font)
  (+load-ext-font)
  (+load-emoji-font))

(+load-font)

;; all-the-icons 和 fire-code-mode 只能在 GUI 模式下使用。
(when (display-graphic-p)
  (use-package all-the-icons :demand)
  (use-package fira-code-mode
	:custom
	(fira-code-mode-disabled-ligatures '("[]" "#{" "#(" "#_" "#_(" "x"))
	:hook prog-mode))
  #+end_src

+ 查看 Emacs 支持的字体名称： =(print (font-family-list))=
+ 安装、更新 FiraCode Symbol 字体： =M-x fira-code-mode-install-fonts=
+ 安装、更新 Icon 字体： =M-x all-the-icons-install-fonts=
+ 参考: https://github.com/DogLooksGood/dogEmacs/blob/master/elisp/init-font.el
  
* completion
** vertico

Vertico 基于默认完成提供一个高性能且简约的垂直完成 UI 系统。Vertico 经过复用内置设施系统，Vertico 实现了与内置
Emacs 补全的完全兼容命令和完成表。Vertico 仅提供完成 UI，但旨在高度灵活，可扩展和模块化。
+ 如果要插入不存在的对象，例如新建一个 file 或 buffer, 可以使用 ~M-RET~ 快捷键（vertico-exit-input)；
#+begin_src emacs-lisp
(use-package vertico
  :straight (:repo "minad/vertico" :files ("*" "extensions/*.el" (:exclude ".git")))
  :hook
  ;; 在输入时清理文件路径。
  (rfn-eshadow-update-overlay . vertico-directory-tidy)
  :config
  ;; 显示的侯选者数量。
  (setq vertico-count 20)
  (setq vertico-cycle nil)
  (vertico-mode 1)
  ;; 文件路径操作。
  (define-key vertico-map (kbd "<backspace>") #'vertico-directory-delete-char)
  (define-key vertico-map (kbd "RET") #'vertico-directory-enter))

(use-package emacs
  :init
  ;; 在 minibuffer 中不显示光标。
  (setq minibuffer-prompt-properties '(read-only t cursor-intangible t face minibuffer-prompt))
  (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode)
  ;; M-x 是不显示当前 mode 不支持的命令以及 vertico 相关命令。
  (setq read-extended-command-predicate #'command-completion-default-include-p)
  ;; 开启 minibuffer 递归编辑。
  (setq enable-recursive-minibuffers t))
#+end_src

** orderless

这个包提供名为 orderless 补全风格，它使用空格分割匹配模式，模式的顺序没有关系，但是 AND 关系。各模式可以使用如
下几种类型：
1. 字面意思(literally);
2. 正则表达式(regexp);
3. 首字母缩写(initialism);
4. flex 样式或多个单词前缀。

默认情况下，启用正则表达式和文字匹配。

orderless 过滤 vertico 提供的候选者, [[https://github.com/minad/consult/wiki][支持多种 dispatch 组合]], 如 =!zhangjun hang$=:
+ ~~flex flex~~
+ ~=literal literal=~
+ ~%char-fold char-fold%~
+ ~`initialism initialism`~
+ ~!without-literal without-literal!~ 
+ ~.ext(file extension)~
+ ~regexp$(regexp matching at end)~

#+begin_src  emacs-lisp
(use-package orderless
  :config
  ;; @minad’s orderless configuration: https://github.com/minad/consult/wiki#minads-orderless-configuration
  (defvar +orderless-dispatch-alist
    '((?% . char-fold-to-regexp)
      (?! . orderless-without-literal)
      (?`. orderless-initialism)
      (?= . orderless-literal)
      (?~ . orderless-flex)))

  (defun +orderless--suffix-regexp ()
    (if (and (boundp 'consult--tofu-char) (boundp 'consult--tofu-range))
        (format "[%c-%c]*$"
                consult--tofu-char
                (+ consult--tofu-char consult--tofu-range -1))
      "$"))

  ;; Recognizes the following patterns:
  ;; * ~flex flex~
  ;; * =literal literal=
  ;; * %char-fold char-fold%
  ;; * `initialism initialism`
  ;; * !without-literal without-literal!
  ;; * .ext (file extension)
  ;; * regexp$ (regexp matching at end)
  (defun +orderless-dispatch (word _index _total)
    (cond
     ;; Ensure that $ works with Consult commands, which add disambiguation suffixes
     ((string-suffix-p "$" word)
      `(orderless-regexp . ,(concat (substring word 0 -1) (+orderless--suffix-regexp))))
     ;; File extensions
     ((and (or minibuffer-completing-file-name
               (derived-mode-p 'eshell-mode))
           (string-match-p "\\`\\.." word))
      `(orderless-regexp . ,(concat "\\." (substring word 1) (+orderless--suffix-regexp))))
     ;; Ignore single !
     ((equal "!" word) `(orderless-literal . ""))
     ;; Prefix and suffix
     ((if-let (x (assq (aref word 0) +orderless-dispatch-alist))
          (cons (cdr x) (substring word 1))
        (when-let (x (assq (aref word (1- (length word))) +orderless-dispatch-alist))
          (cons (cdr x) (substring word 0 -1)))))))
  ;; 使用上面自定义的 dispatch。
  (setq orderless-style-dispatchers '(+orderless-dispatch))
  
  ;; 自定义名为 +orderless-with-initialism 的 orderless 风格。
  (orderless-define-completion-style +orderless-with-initialism
    (orderless-matching-styles '(orderless-initialism orderless-literal orderless-regexp)))
  ;; 使用 orderless 和 emacs 原生的 basic 补全风格， 且 orderless 的优先级更高。
  (setq completion-styles '(orderless basic))
  (setq completion-category-defaults nil)
  ;; 进一步设置各 category 使用的补全风格。
  (setq completion-category-overrides
        '(;; buffer name 补全
          (buffer (styles +orderless-with-initialism)) 
          ;; file path&name 补全, partial-completion 提供了 wildcard 支持。
          (file (styles partial-completion basic)) 
          ;; M-x Command 补全
          (command (styles +orderless-with-initialism)) 
          ;; variable 补全
          (variable (styles +orderless-with-initialism))
          ;; symbol 补全
          (symbol (styles +orderless-with-initialism)))) 
  ;; 使用 SPACE 来分割过滤字符串, SPACE 可以用 \ 转义。
  (setq orderless-component-separator #'orderless-escapable-split-on-space))
#+end_src

** consult

安装 ripgrep 工具命令：
#+begin_src bash :tangle ~/.emacs.d/init.sh
which rg || brew install ripgrep
#+end_src

#+begin_src  emacs-lisp
(use-package consult
  :straight (consult :host github :repo "minad/consult")
  :hook
  (completion-list-mode . consult-preview-at-point-mode)
  :init
  ;; 如果搜索字符少于 3，可以添加后缀#开始搜索，如 #gr#。
  (setq consult-async-min-input 3)
  ;; 从头开始搜索（而非前位置）。
  (setq consult-line-start-from-top t)
  ;; 预览寄存器。
  (setq register-preview-function #'consult-register-format)
  (advice-add #'register-preview :override #'consult-register-window)
  ;; 引用定义和跳转。
  (setq xref-show-xrefs-function #'consult-xref)
  (setq xref-show-definitions-function #'consult-xref)
  :config
  ;; 按 C-l 激活预览，否则 Buffer 列表中有大文件或远程文件时会卡住。
  (setq consult-preview-key "C-l")
  (setq completion-in-region-function #'consult-completion-in-region)
  ;; 不对 consult-line 结果进行排序（按行号排序）。
  (consult-customize consult-line :prompt "Search: " :sort nil)
  ;; Buffer 列表中不显示的 Buffer 名称。
  (mapcar 
   (lambda (pattern) (add-to-list 'consult-buffer-filter pattern))
   '("\\*scratch\\*" 
     "\\*Warnings\\*"
     "\\*helpful.*"
     "\\*Help\\*" 
     "\\*Org Src.*"
     "Pfuture-Callback.*"
     "\\*epc con"
     "\\*dashboard"
     "\\*Ibuffer"
     "\\*sort-tab"
     "\\*Google Translate\\*"
     "\\*straight-process\\*"
     "\\*Native-compile-Log\\*"     
     "[0-9]+.gpg")))

;; consult line 时自动展开 org 内容。
;; https://github.com/minad/consult/issues/563#issuecomment-1186612641
(defun my/org-show-entry (fn &rest args)
  (interactive)
  (when-let ((pos (apply fn args)))
    (when (derived-mode-p 'org-mode)
      (org-fold-show-entry))))
(advice-add 'consult-line :around #'my/org-show-entry)

;;; consult
;; C-c 绑定 (mode-specific-map)
(global-set-key (kbd "C-c M-x") #'consult-mode-command)
(global-set-key (kbd "C-c i") #'consult-info)
(global-set-key (kbd "C-c m") #'consult-man)
;; C-x 绑定 (ctl-x-map)
;; 使用 savehist 持久化保存的 minibuffer 历史。
(global-set-key (kbd "C-M-;") #'consult-complex-command) 
(global-set-key (kbd "C-x b") #'consult-buffer)
(global-set-key (kbd "C-x 4 b") #'consult-buffer-other-window)
(global-set-key (kbd "C-x 5 b") #'consult-buffer-other-frame)
(global-set-key (kbd "C-x r b") #'consult-bookmark)
(global-set-key (kbd "C-x p b") #'consult-project-buffer)
;; 寄存器绑定。
(global-set-key (kbd "C-'") #'consult-register-store)
(global-set-key (kbd "C-M-'") #'consult-register)
;; 其它自定义绑定。
(global-set-key (kbd "M-y") #'consult-yank-pop)
(global-set-key (kbd "M-Y") #'consult-yank-from-kill-ring)
;; M-g 绑定 (goto-map)
(global-set-key (kbd "M-g e") #'consult-compile-error)
;;(global-set-key (kbd "M-g f") #'consult-flycheck)
(global-set-key (kbd "M-g g") #'consult-goto-line)
(global-set-key (kbd "M-g o") #'consult-outline)
;; consult-buffer 默认已包含 recent file.
;;(global-set-key (kbd "M-g r") #'consult-recent-file)
(global-set-key (kbd "M-g m") #'consult-mark)
(global-set-key (kbd "M-g k") #'consult-global-mark)
(global-set-key (kbd "M-g i") #'consult-imenu)
(global-set-key (kbd "M-g I") #'consult-imenu-multi)
;; M-s 绑定 (search-map)
(global-set-key (kbd "M-s d") #'consult-find)
(global-set-key (kbd "M-s D") #'consult-locate)
(global-set-key (kbd "M-s g") #'consult-grep)
(global-set-key (kbd "M-s G") #'consult-git-grep)
(global-set-key (kbd "M-s r") #'consult-ripgrep)
(global-set-key (kbd "M-s l") #'consult-line)
(global-set-key (kbd "M-s M-l") #'consult-line)
(global-set-key (kbd "M-s L") #'consult-line-multi)
;; Isearch 集成。
(global-set-key (kbd "M-s e") #'consult-isearch-history)
;;:map isearch-mode-map
(define-key isearch-mode-map (kbd "M-e") #'consult-isearch-history)
(define-key isearch-mode-map (kbd "M-s e") #'consult-isearch-history)
(define-key isearch-mode-map (kbd "M-s l") #'consult-line)
(define-key isearch-mode-map (kbd "M-s L") #'consult-line-multi)
;; Minibuffer 历史。
;;:map minibuffer-local-map)
(define-key minibuffer-local-map (kbd "M-s") #'consult-history)
(define-key minibuffer-local-map (kbd "M-r") #'consult-history)
#+end_src
+ =consult-buffer= 显示的 File 列表来源于变量 =recentf-list=;
+ 需要修改 consult.el 来避免 string-width value 未定义的情况：https://github.com/minad/consult/commit/921e9

** embark

#+begin_src emacs-lisp
(use-package embark
  :straight (embark :files ("*.el"))
  :init
  ;; 使用 C-h 来显示 key preifx 绑定。
  (setq prefix-help-command #'embark-prefix-help-command)
  (setq embark-quit-after-action nil)
  :config
  (setq embark-prompter 'embark-keymap-prompter)
  ;; 隐藏 Embark live/completions buffers 的 modeline.
  (add-to-list 'display-buffer-alist
               '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                 nil
                 (window-parameters (mode-line-format . none))))
  (global-set-key (kbd "C-;") #'embark-act)
  (define-key global-map [remap describe-bindings] #'embark-bindings))

(use-package embark-consult :after (embark consult)  :hook  (embark-collect-mode . consult-preview-at-point-mode))
#+end_src
+ 使用 gnu find 命令, 需要加环境变量 ~export PATH="/usr/local/opt/findutils/libexec/gnubin:$PATH"~
+ embark-consult 支持 embark 和 consult 集成，如使用 wgrep 编辑 consult grep/line 的 export 的结果。

** marginalia

#+begin_src  emacs-lisp
(use-package marginalia
  :init
  ;; 显示绝对时间。
  (setq marginalia-max-relative-age 0)
  (marginalia-mode)
  :config
  ;; 文件不添加大小，修改时间等注释，防止 tramp 时卡住。
  (setq marginalia-annotator-registry (assq-delete-all 'file marginalia-annotator-registry))
  (setq marginalia-annotator-registry (assq-delete-all 'project-file marginalia-annotator-registry)))
#+end_src

** yasnippet

#+begin_src emacs-lisp
(use-package yasnippet
  :init
  (defvar snippet-directory "~/.emacs.d/snippets")
  :hook
  ((prog-mode org-mode  vterm-mode) . yas-minor-mode)
  :config
  (add-to-list 'yas-snippet-dirs snippet-directory)
  ;; 保留 snippet 的缩进。
  (setq yas-indent-line 'fixed)
  (yas-global-mode 1))

(use-package consult-yasnippet
  :defer
  :after(consult yasnippet)
  :config
  (define-key yas-minor-mode-map (kbd "C-c y") #'consult-yasnippet))

;; 避免报错：Symbol’s function definition is void: yasnippet-snippets--fixed-indent
(use-package yasnippet-snippets :after(yasnippet))
#+end_src
+ 完全输入 =snippet= 简写后，按 =TAB= 自动扩展。
+ 从 [[https://github.com/AndreaCrotti/yasnippet-snippets][yasnippet-snippets]] 获取 snippets, 然后放到 snippet-directory 目录下，这样更容易定制。

* dired

安装 coreutils 包：
#+begin_src bash :tangle ~/.emacs.d/init.sh
which tac || brew install coreutils
#+end_src

#+begin_src emacs-lisp
(use-package dired
  :straight (:type built-in)
  :config
  ;; re-use dired buffer, available in Emacs 28, @see https://debbugs.gnu.org/cgi/bugreport.cgi?bug=20598
  (setq dired-kill-when-opening-new-dired-buffer t)
  ;; if another Dired buffer is visible in another window, use that directory as target for Rename/Copy
  (setq dired-dwim-target t)
  ;; @see https://emacs.stackexchange.com/questions/5649/sort-file-names-numbered-in-dired/5650#5650
  ;; 下面的参数只对安装了 coreutils (brew install coreutils) 的包有效，否则会报错。
  (setq dired-listing-switches "-laGh1v --group-directories-first")
  (put 'dired-find-alternate-file 'disabled nil))

;; dired 显示高亮增强。
(use-package diredfl :config (diredfl-global-mode))
#+end_src

* grep

设置 ~grep/ripgrep~ 忽略的目录和文件:
#+begin_src emacs-lisp
(use-package grep
  :config
  (setq grep-highlight-matches t)
  (setq grep-find-ignored-directories
	(append
	 (list
          ".git"
          ".hg"
          ".idea"
          ".project"
          ".settings"
          ".svn"
          "bootstrap*"
          "pyenv"
          "target"
          ".cache"
          "vendor"
          "node_modules"
        )
	 grep-find-ignored-directories))
  (setq grep-find-ignored-files
	(append
	 (list
          "*.blob"
          "*.gz"
          "*.jar"
          "*.xd"
          "TAGS"
          "dependency-reduced-pom.xml"
          "projectile.cache"
          "workbench.xmi"
          "GPATH"
          "GRTAGS"
          "GTAGS"
          ".TAGS"
          "TAGS"
          ".classpath"
          ".project"
          ".DS_Store"
          )
	 grep-find-ignored-files)))

;; 编辑 grep buffers, 可以和 consult-grep 和 embark-export 联合使用。
(use-package wgrep)

(global-set-key "\C-cn" 'find-dired)
(global-set-key "\C-cN" 'grep-find)
#+end_src

ctrlf 是 isearch 更好的替代品（搜索结果编号，支持：C-v, M-v, M-<, M->, C-l）:
#+begin_src emacs-lisp
(use-package ctrlf
  :config
  (ctrlf-mode +1)
  (add-hook 'pdf-isearch-minor-mode-hook (lambda () (ctrlf-local-mode -1))))
#+end_src

在线搜索：
+ 搜索前缀命令： =C-c s= , 可以先选中 region 再执行上面的搜索。
+ 修复启动报错: =rm ~/.emacs.d/elpa/engine-mode*/engine-mode-*.el*= 。
#+begin_src emacs-lisp
;; browser-url 使用 Mac 默认浏览器。
(setq browse-url-browser-function 'browse-url-default-macosx-browser)
;;(setq browse-url-browser-function 'xwidget-webkit-browse-url)
(setq xwidget-webkit-cookie-file "~/.emacs.d/cookie.txt")
(setq xwidget-webkit-buffer-name-format "*webkit: %T")

(use-package engine-mode
  :config
  (engine/set-keymap-prefix (kbd "C-c s"))
  (engine-mode t)
  ;;(setq engine/browser-function 'eww-browse-url)
  (defengine github "https://github.com/search?ref=simplesearch&q=%s" :keybinding "h")
  (defengine google "http://www.google.com/search?ie=utf-8&oe=utf-8&q=%s" :keybinding "g")
  (defengine twitter "https://twitter.com/search?q=%s" :keybinding "t")
  (defengine wikipedia "http://www.wikipedia.org/search-redirect.php?language=en&go=Go&search=%s" :keybinding "w" :docstring "Searchin' the wikis."))
#+end_src

* pyim

#+begin_src emacs-lisp
(setq-default default-input-method "pyim")

(use-package pyim
  :straight (pyim :repo "tumashu/pyim")
  :hook
  ;; 设置缺省输入法为 pyim。
  (emacs-startup . (lambda () (setq default-input-method "pyim")))
  :config
  (setq pyim-title "IM")
  (setq pyim-indicator-modeline-string '("CN" "EN"))
  ;; 单字符快捷键，可以实现快速切换标点符号和添加个人生词。
  (setq pyim-outcome-trigger "^")
  (setq pyim-dcache-directory "~/.emacs.d/sync/pyim/dcache/")
  ;; 使用全拼。
  (pyim-default-scheme 'quanpin)
  ;; 使用百度云拼音。
  (setq pyim-cloudim 'baidu)
  ;; 关闭代码搜索中文功能（比如拼音，五笔码等）， 会导致 isearch 卡死。
  (pyim-isearch-mode nil)
  ;; 中文使用全角标点，英文使用半角标点。
  (setq-default pyim-punctuation-translate-p '(auto yes no))
  ;; posframe 性能更好且显式的较为干净, popup 较慢且容易干扰当前 buffer。
  (setq-default pyim-page-tooltip 'posframe)
  ;; 设置模糊音。
  (setq pyim-pinyin-fuzzy-alist '( ("z" "zh") ("c" "ch") ("s" "sh") ("en" "eng") ("in" "ing")))
  ;; Dictionaries:
  ;;   pyim-greatdict is not recommended. It has too many useless words and slows down pyim.
  ;;
  ;;   Download pyim-bigdict,
  ;;   curl -L http://tumashu.github.io/pyim-bigdict/pyim-bigdict.pyim.gz | zcat > ~/.eim/pyim-bigdict.pyim
  ;;
  ;;   Download pyim-tsinghua (recommended),
  ;;   curl -L https://raw.githubusercontent.com/redguardtoo/pyim-tsinghua-dict/master/pyim-tsinghua-dict.pyim > ~/.eim/pyim-tsinghua-dict.pyim
  (setq pyim-dicts '(
                     (:name "tsinghua" :file "~/.emacs.d/straight/repos/pyim-tsinghua-dict/pyim-tsinghua-dict.pyim")
                     ;; (:name "pyim-bigdict" :file "~/.emacs.d/sync/pyim/pyim-bigdict.pyim")
                     ))
  ;; 使用性能更好的 pyim-dregcache dcache 后端。
  ;;(setq pyim-dcache-backend 'pyim-dregcache)
  ;; 设置中英文自动切换。
  (setq-default pyim-english-input-switch-functions
    	        '(pyim-probe-program-mode
    	          pyim-probe-auto-english
    	          pyim-probe-dynamic-english
    	          ;; pyim-probe-org-structure-template
    	          ))
  ;; 显示候选词数量。
  (setq pyim-page-length 8)
  ;; 输入法切换。
  (global-set-key (kbd "C-\\") #'toggle-input-method)
  ;; 中英文切换。
  (global-set-key (kbd "C-.") #'pyim-toggle-input-ascii)
  ;; 金手指设置，将光标处的拼音字符串转换为中文。
  (global-set-key (kbd "M-j") #'pyim-convert-string-at-point))

;; 清华大学开放中文词库。
(use-package pyim-tsinghua-dict
  :straight (pyim-tsinghua-dict :host github :repo "redguardtoo/pyim-tsinghua-dict")
  :after pyim
  :config
  (pyim-tsinghua-dict-enable))
#+end_src

* org
** org

#+begin_src bash :tangle ~/.emacs.d/init.sh
# org
which watchexec || brew install watchexec
which pygmentize || brew install pygments
which magick || brew install imagemagick
#+end_src

#+begin_src emacs-lisp
(use-package org
  ;;:straight (org :repo "https://git.savannah.gnu.org/git/emacs/org-mode.git")
  :ensure auctex
  :config
  (setq org-ellipsis " ⭍" ;; ".."
        ;; 使用 UTF-8 显示 LaTeX 或 \xxx 特殊字符， M-x org-entities-help 查看所有特殊字符。
        org-pretty-entities t
        org-highlight-latex-and-related '(latex)
        ;; 只显示而不处理和解释 latex 标记，例如 \xxx 或 \being{xxx}, 避免 export pdf 时出错。
        org-export-with-latex 'verbatim
        ;; 隐藏标记。
        org-hide-emphasis-markers t
        ;; 去掉 * 和 /, 使它们不再具有强调含义。
        org-emphasis-alist
        '(("_" underline)
          ("=" org-verbatim verbatim)
          ("~" org-code verbatim)
          ("+" (:strike-through t)))
        ;; 隐藏 block
        org-hide-block-startup t
        org-hidden-keywords '(title)
        org-cycle-separator-lines 2
        org-cycle-level-faces t
        org-n-level-faces 4
        ;; TODO 状态更新记录到 LOGBOOK Drawer 中。
        org-log-into-drawer t
        ;; TODO 状态更新时记录 note.
        org-log-done 'note ;; note, time
        ;; 默认显示 inline image.
        org-startup-with-inline-images t
        ;; 先从 #+ATTR.* 获取宽度，如果没有设置则默认为 300 。
        org-image-actual-width '(300)
        ;; cycle headline 时显示 image.
        org-cycle-inline-images-display t
        org-export-with-broken-links t
        ;; 文件链接使用相对路径, 解决 hugo 等 image 引用的问题。
        org-link-file-path-type 'relative
        org-startup-folded 'content
        ;; 使用 R_{s} 形式的下标（默认是 R_s, 容易与正常内容混淆) 。
        org-use-sub-superscripts nil
        ;; headerline 默认加序号。
        org-startup-numerated t
        ;; export 时不处理 super/subscripting, 等效于 #+OPTIONS: ^:nil 。
        org-export-with-sub-superscripts nil
        ;; heaerline 不显示 *。
        org-hide-leading-stars t
        ;; 缩进 4 个字符。
        org-indent-indentation-per-level 4
        ;; 内容缩进与对应 headerline 一致。
        org-adapt-indentation t
        org-startup-indented t)
  ;;(setq org-fold-core-style 'overlays)
  (setq org-tags-column 0)
  (setq  org-auto-align-tags nil)
  ;; 显示不可见的编辑。
  (setq org-catch-invisible-edits 'show-and-error)
  (setq org-fold-catch-invisible-edits t)
  (setq org-insert-heading-respect-content t)
  ;; 支持 ID property 作为 internal link target(默认是 CUSTOM_ID property)
  (setq org-id-link-to-org-use-id t)
  ;; 光标位于 section 中间时不 split line.
  (setq org-M-RET-may-split-line nil)
  (setq org-todo-keywords
        '((sequence "☞ TODO(t@)" "⚔ INPROCESS(s@)" "⚑ WAITING(w!)" "|" "☟ NEXT(n)" "✰ Important(i!)" "✔ DONE(d!)" "✘ CANCELED(c!)")
          (sequence "✍ NOTE(N)" "FIXME(f)" "☕ BREAK(b)" "❤ Love(l)" "REVIEW(r)" )))
  (add-hook 'org-mode-hook 'turn-on-auto-fill)
  (add-hook 'org-mode-hook (lambda () (display-line-numbers-mode 0))))

;; 关闭与 pyim 冲突的 C-, 快捷键。
(define-key org-mode-map (kbd "C-,") nil)
(define-key org-mode-map (kbd "C-'") nil)
;; 关闭容易误碰的按键。
;; (define-key org-mode-map (kbd "C-c C-x a") nil)
;; (define-key org-mode-map (kbd "C-c C-x A") nil)
;; (define-key org-mode-map (kbd "C-c C-x C-s") nil)
;; 全局快捷键。
(global-set-key (kbd "C-c l") #'org-store-link)
(global-set-key (kbd "C-c a") #'org-agenda)
(global-set-key (kbd "C-c c") #'org-capture)
(global-set-key (kbd "C-c b") #'org-switchb)

;; C-u C-c l 获得文件链接时包含行号。
(defun my-link-to-line-number ()
  (number-to-string (org-current-line)))
(add-hook 'org-create-file-search-functions 'my-link-to-line-number)

;; 自动创建和更新目录。
(use-package org-make-toc
  :disabled
  :config
  (add-hook 'org-mode-hook #'org-make-toc-mode))

;; 关闭频繁弹出的 org-element-cache 警告 buffer 。
;;(setq warning-suppress-types (append warning-suppress-types '((org-element-cache))))
#+END_SRC
+ pygments 实现 Latex PDF 代码语法高亮；
+ imagemagick 用于图片分辨率装换；
  
** face

#+begin_src emacs-lisp
(defun my/org-faces ()
  (setq-default line-spacing 2)
  (dolist (face '((org-level-1 . 1.2)
                  (org-level-2 . 1.1)
                  (org-level-3 . 1.05)
                  (org-level-4 . 1.0)
                  (org-level-5 . 1.1)
                  (org-level-6 . 1.1)
                  (org-level-7 . 1.1)
                  (org-level-8 . 1.1)))
    (set-face-attribute (car face) nil :height (cdr face)))
  ;; 美化 BEGIN_SRC 整行。
  (setq org-fontify-whole-block-delimiter-line t)
  ;; 如果配置参数 :inherit 'fixed-pitch, 则需要明确设置 fixed-pitch 字体，否则选择的缺省字体可能导致显示问题。
  (custom-theme-set-faces
   'user
   '(org-block ((t (:height 0.9))))
   '(org-code ((t (:height 0.9))))
   ;; 调小高度 , 并设置下划线。
   '(org-block-begin-line ((t (:height 0.8 :underline "#A7A6AA"))))
   '(org-block-end-line ((t (:height 0.8 :underline "#A7A6AA"))))
   '(org-meta-line ((t (:height 0.7))))
   '(org-document-info-keyword ((t (:height 0.6))))
   '(org-document-info ((t (:height 0.8))))
   '(org-document-title ((t (:foreground "#ffb86c" :weight bold :height 1.5))))
   '(org-link ((t (:foreground "royal blue" :underline t))))
   '(org-property-value ((t (:height 0.8))) t)
   '(org-drawer ((t (:height 0.8))) t)
   '(org-special-keyword ((t (:height 0.8 :inherit 'fixed-pitch))))
   ;; table 使用中英文严格等宽的 Sarasa Mono SC 字体, 避免中英文不对齐。
   '(org-table ((t (:font "Sarasa Mono SC" :height 0.9))))
   '(org-verbatim ((t (:height 0.9))))
   '(org-tag ((t (:weight bold :height 0.8))))
   '(org-todo ((t (:inherit 'fixed-pitch))))
   '(org-done ((t (:inherit 'fixed-pitch))))
   '(org-ellipsis ((t (:inherit 'fixed-pitch))))
   '(org-property-value ((t (:inherit 'fixed-pitch))))))
(add-hook 'org-mode-hook 'my/org-faces)
(add-hook 'org-mode-hook 'prettify-symbols-mode)
(setq-default prettify-symbols-alist '(("#+BEGIN_SRC" . "»") ("#+END_SRC" . "«") ("#+begin_src" . "»") ("#+end_src" . "«")))
(setq prettify-symbols-unprettify-at-point 'right-edge)

(use-package org-superstar
  :after (org)
  :hook
  (org-mode . org-superstar-mode)
  :custom
  (org-superstar-remove-leading-stars t)
  (org-superstar-headline-bullets-list '("◉"  "🞛" "✿" "○" "▷"))
  (org-superstar-headline-bullets-list '("☰" "☱" "☲" "☳" "☴" "☵" "☶" "☷"))
  (org-superstar-item-bullet-alist '((43 . "⬧") (45 . "⬨")))
  :custom-face
  (org-superstar-item ((t (:inherit 'fixed-pitch))))
  (org-superstar-header-bullet ((t (:height 200 :inherit 'fixed-pitch)))))

(use-package org-fancy-priorities
  :ensure t
  :hook
  (org-mode . org-fancy-priorities-mode)
  :config
  ;; org 默认最低优先级是 C, 这里加一级。
  (setq org-priority-lowest ?D)
  (setq org-fancy-priorities-list '("[⚡A]" "[⬆B]" "[⬇C]" "[☕D]")))

;; 编辑时显示隐藏的标记。
(use-package org-appear
  :config
  (add-hook 'org-mode-hook 'org-appear-mode)
  ;; 删除 * 和 / 类型的标记。
  (setq org-appear-elements '(underline strike-through verbatim code)))
#+end_src
    
** fill

内容居中显示:
#+begin_src emacs-lisp
(defun my/org-mode-visual-fill (fill width)
  (setq-default
   ;; 自动换行的字符数。
   fill-column fill
   ;; window 可视化行宽度，值应该比 fill-column 大，否则超出的字符被隐藏。
   visual-fill-column-width width
   visual-fill-column-fringes-outside-margins nil
   ;; 使用 setq-default 来设置居中, 否则可能不生效。
   visual-fill-column-center-text t)
  (visual-fill-column-mode 1))

(use-package visual-fill-column
  :after (org)
  :hook
  (org-mode . (lambda () (my/org-mode-visual-fill 110 130)))
  :config
  ;; 文字缩放时自动调整 visual-fill-column-width 。
  (advice-add 'text-scale-adjust :after #'visual-fill-column-adjust))
#+end_src
+ 如果文字居中失效, 可以执行 =M-x redraw-display= 命令生效。

** image

#+begin_src bash :tangle ~/.emacs.d/init.sh
which pngpaste || brew install pngpaste
#+end_src

拖拽保存图片或 F6 保存剪贴板中图片:
#+begin_src emacs-lisp
(use-package org-download
  :config
  (setq-default org-download-image-dir "./images/")
  (setq org-download-method 'directory
        org-download-display-inline-images 'posframe
        org-download-screenshot-method "pngpaste %s"
        org-download-image-attr-list '("#+ATTR_HTML: :width 400 :align center"))
  (add-hook 'dired-mode-hook 'org-download-enable)
  (org-download-enable)
  (global-set-key (kbd "<f6>") #'org-download-screenshot))
#+end_src

** babel

#+begin_src emacs-lisp
;; eval 前需要确认。
(setq org-confirm-babel-evaluate t)
;; 关闭 C-c C-c 触发 eval code.
;;(setq org-babel-no-eval-on-ctrl-c-ctrl-c nil)
(setq org-src-fontify-natively t)
(setq org-src-tab-acts-natively t)
;; 为 #+begin_quote 和  #+begin_verse 添加特殊 face 。
(setq org-fontify-quote-and-verse-blocks t)
;; 不自动缩进。
(setq org-src-preserve-indentation t)
(setq org-edit-src-content-indentation 0)
;; 在当前窗口编辑 SRC Block.
(setq org-src-window-setup 'current-window)
;; export 输出类型。
(setq org-export-backends '(go md gfm html latex man))

(require 'org)
;; org bable 完整支持的语言列表（ob- 开头的文件）：
;; https://git.savannah.gnu.org/cgit/emacs/org-mode.git/tree/lisp
;; 对于官方不支持的语言，可以通过 use-pacakge 来安装。
(use-package ob-go) ;; golang 
(use-package ox-reveal) ;; reveal.js
(use-package ox-gfm) ;; github flavor markdown
;; 启用的 org babel 的语言列表。
(org-babel-do-load-languages
 'org-babel-load-languages
 '((shell . t)
   (js . t)
   (java . t)
   (makefile . t)
   (go . t)
   (emacs-lisp . t)
   (python . t)
   (sed . t)
   (awk . t)
   (dot . t)
   (css . t)))

(use-package org-contrib
  :straight (org-contrib :repo "https://git.sr.ht/~bzg/org-contrib"))
#+end_src

** tex

在 org 文档的头部添加参数：
#+begin_verse :tangle no
#+LATEX_COMPILER: xelatex
#+LATEX_CLASS: ctexart
#+LATEX_HEADER: \usepackage{mystyle}
#+OPTIONS: prop:t ^:nil
#+LANGUAGE: zh-CN
#+end_verse

#+begin_src emacs-lisp
(require 'ox-latex)
(with-eval-after-load 'ox-latex
  ;; latex image 的默认宽度, 可以通过 #+ATTR_LATEX :width xx 配置。
  (setq org-latex-image-default-width "0.7\\linewidth")
  ;; 使用 booktabs style 来显示表格，例如支持隔行颜色, 这样 #+ATTR_LATEX: 中不需要添加 :booktabs t。
  (setq org-latex-tables-booktabs t)
  ;; 保存 LaTeX 日志文件。
  (setq org-latex-remove-logfiles nil)
  ;; 使用 minted 而非默认的 listings, 支持更多的语言类型，否则编译时报错。
  (setq org-latex-listings 'minted)
  ;; 目录页前后分页。
  (setq org-latex-toc-command "\\clearpage \\tableofcontents \\clearpage")
  ;; 使用支持中文的 xelatex。
  (setq org-latex-pdf-process '("latexmk -xelatex -quiet -shell-escape -f %f"))
  ;; ;; Alist of packages to be inserted in every LaTeX header.
  ;; (setq org-latex-packages-alist
  ;;       (quote (("" "color" t)
  ;;               ("" "xcolor" t)
  ;;               ("" "listings" t)
  ;;               ("" "fontspec" t)
  ;;               ("" "parskip" t) ;; 增加正文段落的间距
  ;;               ("AUTO" "inputenc" t))))
  (add-to-list 'org-latex-classes
               '("ctexart"
                 "\\documentclass[lang=cn,11pt,a4paper,table]{ctexart}
                 [NO-DEFAULT-PACKAGES]
                 [PACKAGES]
                 [EXTRA]"
                 ("\\section{%s}" . "\\section*{%s}")
                 ("\\subsection{%s}" . "\\subsection*{%s}")
                 ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                 ("\\paragraph{%s}" . "\\paragraph*{%s}")
                 ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))
  ;; 自定义 latex 语言环境(基于 tcolorbox), 参考：https://blog.shimanoke.com/ja/posts/output-latex-code-with-tcolorbox/
  (setq org-latex-custom-lang-environments
        '((c "\\begin{programlist}[label={%l}]{c}{: %c}\n%s\\end{programlist}")
          (ditaa "\\begin{programlist}[label={%l}]{text}{: %c}\n%s\\end{programlist}")
          (emacs-lisp "\\begin{programlist}[label={%l}]{lisp}{: %c}\n%s\\end{programlist}")
          (ruby "\\begin{programlist}[label={%l}]{ruby}{: %c}\n%s\\end{programlist}")
          (latex "\\begin{programlist}[label={%l}]{latex}{: %c}\n%s\\end{programlist}")
          (go "\\begin{programlist}[label={%l}]{go}{: %c}\n%s\\end{programlist}")
          (lua "\\begin{programlist}[label={%l}]{lua}{: %c}\n%s\\end{programlist}")
          (java "\\begin{programlist}[label={%l}]{java}{: %c}\n%s\\end{programlist}")
          (javascript "\\begin{programlist}[label={%l}]{javascript}{: %c}\n%s\\end{programlist}")
          (json "\\begin{programlist}[label={%l}]{json}{: %c}\n%s\\end{programlist}")
          (plantuml "\\begin{programlist}[label={%l}]{text}{: %c}\n%s\\end{programlist}")
          (yaml "\\begin{programlist}[label={%l}]{yaml}{: %c}\n%s\\end{programlist}")
          (maxima "\\begin{programlist}[label={%l}]{text}{: %c}\n%s\\end{programlist}")
          (ipython "\\begin{programlist}[label={%l}]{python}{: %c}\n%s\\end{programlist}")
          (python "\\begin{programlist}[label={%l}]{python}{: %c}\n%s\\end{programlist}")
          (perl "\\begin{programlist}[label={%l}]{perl}{: %c}\n%s\\end{programlist}")
          (html "\\begin{programlist}[label={%l}]{html}{: %c}\n%s\\end{programlist}")
          (org "\\begin{programlist}[label={%l}]{text}{: %c}\n%s\\end{programlist}")
          (typescript "\\begin{programlist}[label={%l}]{typescript}{: %c}\n%s\\end{programlist}")
          (scss "\\begin{programlist}[label={%l}]{scss}{: %c}\n%s\\end{programlist}")
          (sh "\\begin{programlist}[label={%l}]{shell}{: %c}\n%s\\end{programlist}")
          (bash "\\begin{programlist}[label={%l}]{shell}{: %c}\n%s\\end{programlist}")
          (shell "\\begin{programlist}[label={%l}]{shell}{: %c}\n%s\\end{programlist}")
          (shellinput "\\begin{shellinput}[%c]\n%s\\end{shellinput}")
          (shelloutput "\\begin{shelloutput}[%c]\n%s\\end{shelloutput}"))))

;; org export html 格式时需要 htmlize.el 包来格式化代码。
(use-package htmlize
  :straight (htmlize :host github :repo "hniksic/emacs-htmlize"))
#+end_src
+ minted 包提供代码语法高亮的功能(TexLive 默认安装), 它依赖 pygements 。
+ 变量 =org-latex-minted-langs= 列出 Emacs Major-Mode 与 minted 语言类型（pygmentize -L lexers）的关系, 如果两者
  一致（如 go-[mod] 和 go), 则不需要列出。
+ minted 的 fontfamily 只对预定义的 tt/courier/helvetica 有效。

自定义样式 mystyle.sty:
#+begin_src latex :tangle  ~/.emacs.d/mystyle.sty
\usepackage{color}
\usepackage{xcolor}
\definecolor{winered}{rgb}{0.5,0,0}
\definecolor{lightgrey}{rgb}{0.9,0.9,0.9}
\definecolor{tableheadcolor}{gray}{0.92}
\definecolor{commentcolor}{RGB}{0,100,0}
\definecolor{frenchplum}{RGB}{190,20,83}

% 安装荧光笔效果的强调宏包 breakfbox(https://blog.shimanoke.com/ja/posts/change-latex-emph/)
% 1. 克隆 https://github.com/doraTeX/breakfbox 到 /usr/local/texlive/texmf-local/tex/latex
% 2. 刷新数据库:  sudo mktexlsr

% 黄色背景高亮强调（来源于 breakfbox)
\usepackage{uline--}
\renewcommand{\emph}[1]{
  {\sffamily\bfseries\itshape
    \uline[
      background,
      color={[rgb]{1,1,0.0}},
      width=0.8em,position=1pt]{#1}}}

% 自定义 programlist 语言环境
% https://blog.shimanoke.com/ja/posts/output-latex-code-with-tcolorbox/
\usepackage{tcolorbox}
\tcbuselibrary{breakable,skins,raster,external,listings,minted}
\tcbEXTERNALIZE
\newtcblisting[
  auto counter,
  number within=section]{programlist}[3][]{
  listing engine=minted,
  minted style=emacs,
  minted language=#2,
  minted options={autogobble,fontsize=\footnotesize,breaklines,breakanywhere,baselinestretch=1.2,linenos,numbersep=3mm},
  % 不显示 title
  %title={\sffamily\bfseries 代码块 \thetcbcounter},
  %title={\sffamily\bfseries 代码块 \thetcbcounter #3},
  after,
  breakable=true,
  lowerbox=ignored,
  hyphenationfix=true,
  %边框颜色
  %colback=blue!5!white,
  %colframe=blue!85!black,
  listing only,
  enhanced,
  drop fuzzy shadow southeast,
  left=5mm,
  overlay={\begin{tcbclipinterior}\fill[red!20!blue!20!white] (frame.south west) rectangle ([xshift=5mm]frame.north west);\end{tcbclipinterior}},
  #1
}

% 提示 title
\usepackage[explicit]{titlesec}
\usepackage{titling}
\setlength{\droptitle}{-6em}

% 超链接
\usepackage[colorlinks]{hyperref}
\hypersetup{
  pdfborder={0 0 0},
  colorlinks=true,
  linkcolor={winered},
  urlcolor={winered},
  filecolor={winered},
  citecolor={winered},
  linktoc=all}

% 安装 noto-cjk 中文字体: git clone https://github.com/googlefonts/noto-cjk.git
\usepackage{fontspec}
\usepackage[utf8x]{inputenc}
\setmainfont{Noto Serif SC}
\setsansfont{Noto Sans SC}[Scale=MatchLowercase]
\setmonofont{Noto Sans Mono CJK SC}[Scale=MatchLowercase]
\setCJKmainfont[BoldFont = Noto Serif SC]{Noto Serif SC}
\setCJKsansfont{Noto Sans SC}
\setCJKmonofont{Noto Sans Mono CJK SC}

\XeTeXlinebreaklocale "zh"
\XeTeXlinebreakskip = 0pt plus 1pt minus 0.1pt

% 添加 email
\newcommand\email[1]{\href{mailto:#1}{\nolinkurl{#1}}}

% sidewaytable 依赖 rotfloat
\usepackage {rotfloat}

% tabularx 的特殊 align 参数 X 用来对指定列内容自动换行，表格前需要加如下属性：
% #+ATTR_LATEX: :environment tabularx :booktabs t :width \linewidth :align l|X
\usepackage{tabularx}
% 美化表格显示效果
\usepackage{booktabs}
% 表格隔行颜色, {1} 开始行, {lightgrep} 奇数行颜色, {} 偶数行颜色(空表示白色)
\rowcolors{1}{lightgrey}{}

\usepackage{parskip}
\setlength{\parskip}{1em}
\setlength{\parindent}{0pt}

\usepackage{etoolbox}
\usepackage{calc}

\usepackage[scale=0.85]{geometry}
%\setlength{\headsep}{5pt}

\usepackage{amsthm}
\usepackage{amsmath}
\usepackage{amssymb}
\usepackage{indentfirst}
\usepackage{multicol}
\usepackage{multirow}
\usepackage{linegoal}
\usepackage{graphicx}
\usepackage{fancyvrb}
\usepackage{abstract}
\usepackage{hologo}

\linespread{1.25}
\graphicspath{{image/}{figure/}{fig/}{img/}{images/}}

\usepackage[font=small,labelfont={bf}]{caption}
\captionsetup[table]{skip=3pt}
\captionsetup[figure]{skip=3pt}

% 下划线、强调和删除线等
\usepackage[normalem]{ulem}
% 列表
\usepackage[shortlabels,inline]{enumitem}
\setlist{nolistsep}
% xeCJK 默认会把黑点用汉字显示，而 Noto 没有这个字体，所以显示效果为一个小点。
% 解决办法是将它设置为 \bullet, 这样显示为实心黑点。Windows 带的楷体、仿宋没有这个问题。
\setlist[itemize]{label=$\bullet$}
% 或者：
%\renewcommand\labelitemi{\ensuremath{\bullet}}
#+end_src

** slide

#+begin_src emacs-lisp
(use-package org-tree-slide
  :after (org)
  :commands org-tree-slide-mode
  :hook
  ((org-tree-slide-play . (lambda ()
                            (blink-cursor-mode +1)
                            (setq-default x-stretch-cursor -1)
                            ;;(beacon-mode -1)
                            (redraw-display)
                            (org-display-inline-images)
                            (text-scale-increase 1)
                            ;;(centaur-tabs-mode 0)
                            ;;(awesome-tab-mode 0)
                            (read-only-mode 1)))
   (org-tree-slide-stop . (lambda ()
                            (blink-cursor-mode +1)
                            (setq-default x-stretch-cursor t)
                            (text-scale-increase 0)
                            ;;(beacon-mode +1)
                            ;;(centaur-tabs-mode 1)
                            ;;(awesome-tab-mode 1)
                            (read-only-mode -1))))
  :config
  (setq org-tree-slide-header nil)
  (setq org-tree-slide-heading-emphasis nil)
  (setq org-tree-slide-slide-in-effect t)
  (setq org-tree-slide-content-margin-top 0)
  (setq org-tree-slide-activate-message " ")
  (setq org-tree-slide-deactivate-message " ")
  (setq org-tree-slide-modeline-display nil)
  (setq org-tree-slide-breadcrumbs " 👉 ")
  ;; 隐藏 #+KEYWORD 行内容。
  (defun +org-present-hide-blocks-h ()
    (save-excursion
      (goto-char (point-min))
      (while (re-search-forward "^[[:space:]]*\\(#\\+\\)\\(\\(?:BEGIN\\|END\\|begin\\|end\\|ATTR\\|DOWNLOADED\\)[^[:space:]]+\\).*" nil t)
        (org-flag-region (match-beginning 0) (match-end 0) org-tree-slide-mode t))))
  (add-hook 'org-tree-slide-play-hook #'+org-present-hide-blocks-h)
  (define-key org-mode-map (kbd "<f8>") #'org-tree-slide-mode)
  (define-key org-tree-slide-mode-map (kbd "<f9>") #'org-tree-slide-content)
  (define-key org-tree-slide-mode-map (kbd "<left>") #'org-tree-slide-move-previous-tree)
  (define-key org-tree-slide-mode-map (kbd "<right>") #'org-tree-slide-move-next-tree))
#+end_src
+ 如果文字居中失效, 可以执行 =M-x redraw-display= 命令来生效。

** journal

#+begin_src emacs-lisp
;; 设置缺省 prefix key, 必须在加载 org-journal 前设置。
(setq org-journal-prefix-key "C-c j")

(use-package org-journal
  :commands org-journal-new-entry
  :init
  (defun org-journal-save-entry-and-exit()
    (interactive)
    (save-buffer)
    (kill-buffer-and-window))
  :config
  (define-key org-journal-mode-map (kbd "C-c C-e") #'org-journal-save-entry-and-exit)
  (define-key org-journal-mode-map (kbd "C-c C-j") #'org-journal-new-entry)

  (setq org-journal-file-type 'monthly)
  (setq org-journal-dir "~/journal")
  (setq org-journal-find-file 'find-file)

  ;; 加密 journal 文件。
  (setq org-journal-enable-encryption t)
  (setq org-journal-encrypt-journal t)
  (defun my-old-carryover (old_carryover)
    (save-excursion
      (let ((matcher (cdr (org-make-tags-matcher org-journal-carryover-items))))
        (dolist (entry (reverse old_carryover))
          (save-restriction
            (narrow-to-region (car entry) (cadr entry))
            (goto-char (point-min))
            (org-scan-tags '(lambda ()
                              (org-set-tags ":carried:"))
                           matcher org--matcher-tags-todo-only))))))
  (setq org-journal-handle-old-carryover 'my-old-carryover)

  ;; journal 文件头。
  (defun org-journal-file-header-func (time)
    "Custom function to create journal header."
    (concat
     (pcase org-journal-file-type
       (`daily "#+TITLE: Daily Journal\n#+STARTUP: showeverything")
       (`weekly "#+TITLE: Weekly Journal\n#+STARTUP: folded")
       (`monthly "#+TITLE: Monthly Journal\n#+STARTUP: folded")
       (`yearly "#+TITLE: Yearly Journal\n#+STARTUP: folded"))))
  (setq org-journal-file-header 'org-journal-file-header-func))

;; 修复报错： org-journal-display-entry: Symbol’s value as variable is void: displayed-month
;; https://github.com/bastibe/org-journal/commit/1de9153f2120e92779d95d9e13f249e98ff1ad14
(defun org-journal-display-entry (_arg &optional event)
  "Display journal entry for selected date in another window."
  (interactive
   (list current-prefix-arg last-nonmenu-event))
  (let* ((time (or (ignore-errors (org-journal-calendar-date->time (calendar-cursor-to-date t event)))
                   (org-time-string-to-time (org-read-date nil nil nil "Date:")))))
    ;; (let* ((time (org-journal--calendar-date->time
    ;;               (calendar-cursor-to-date t event))))
    (org-journal-read-or-display-entry time t)))
#+end_src
+ 不开启 org-journal-enable-agenda-integration, 而是向 org-agenda-files 变量添加日志文件的方式。否则在历史日记
  被删除的情况下, 可能导致 Dashbard 显示 agenda 时 hang 。
  
* magit

magit 是全宇宙最强大、最好用的 git 客户端，没有之一！
#+begin_src emacs-lisp
(setq vc-follow-symlinks t)

(use-package magit
  :straight (magit :repo "magit/magit" :files ("lisp/*.el"))
  :custom
  ;; 在当前 window 中显示 magit buffer。
  (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1)
  (magit-log-arguments '("-n256" "--graph" "--decorate" "--color"))
  :config
  ;; kill 所有 magit buffer。
  (defun my-magit-kill-buffers (&rest _)
    "Restore window configuration and kill all Magit buffers."
    (interactive)
    (magit-restore-window-configuration)
    (let ((buffers (magit-mode-get-buffers)))
      (when (eq major-mode 'magit-status-mode)
        (mapc (lambda (buf)
                (with-current-buffer buf
                  (if (and magit-this-process
                           (eq (process-status magit-this-process) 'run))
                      (bury-buffer buf)
                    (kill-buffer buf))))
              buffers))))
  (setq magit-bury-buffer-function #'my-magit-kill-buffers)
  ;; diff org-mode 时展开内容。
  (add-hook 'magit-diff-visit-file-hook (lambda() (when (derived-mode-p 'org-mode)(org-fold-show-entry)))))
#+end_src
+ =(setq auto-revert-check-vc-info t)= 自动 revert buffer，确保 modeline 上的分支名正确，但是 CPU Profile 显示比
  较影响性能，故暂不开启。

git-link 根据仓库地址、commit 等信息为光标位置生成 URL:
#+begin_src emacs-lisp
(use-package git-link :config (setq git-link-use-commit t))
#+end_src

* diff

#+begin_src emacs-lisp
(use-package diff-mode
  :straight (:type built-in)
  :init
  (setq diff-default-read-only t)
  (setq diff-advance-after-apply-hunk t)
  (setq diff-update-on-the-fly t))

(use-package ediff
  :straight (:type built-in)
  :config
  (setq ediff-keep-variants nil)
  (setq ediff-split-window-function 'split-window-horizontally)
  ;; 不创建新的 frame 来显示 Control-Panel。
  (setq ediff-window-setup-function #'ediff-setup-windows-plain))
#+end_src

* coding
** indent

#+begin_src emacs-lisp
;; 显示缩进。
(use-package highlight-indent-guides
  :custom
  (highlight-indent-guides-method 'column)
  (highlight-indent-guides-responsive 'top)
  (highlight-indent-guides-suppress-auto-error t)
  :config
  (add-hook 'python-mode-hook 'highlight-indent-guides-mode)
  (add-hook 'python-ts-mode-hook 'highlight-indent-guides-mode)
  (add-hook 'yaml-mode-hook 'highlight-indent-guides-mode)
  (add-hook 'yaml-ts-mode-hook 'highlight-indent-guides-mode)
  (add-hook 'js-mode-hook 'highlight-indent-guides-mode)
  (add-hook 'js-ts-mode-hook 'highlight-indent-guides-mode)
  (add-hook 'web-mode-hook 'highlight-indent-guides-mode))
#+end_src

** paren

#+begin_src emacs-lisp
;; 彩色括号。
(use-package rainbow-delimiters :hook (prog-mode . rainbow-delimiters-mode))

;; 高亮匹配的括号。
(use-package paren
  :straight (:type built-in)
  :hook (after-init . show-paren-mode)
  :init
  (setq show-paren-when-point-inside-paren t
        show-paren-when-point-in-periphery t)
  ;; Highlight blocks of code in bold
  (setq show-paren-style 'parenthesis) ;; parenthesis, expression
  (set-face-attribute 'show-paren-match nil :weight 'extra-bold))

;; 智能括号。
(use-package smartparens
  :config
  (smartparens-global-mode t)
  (show-smartparens-global-mode t))
#+end_src

** direnv

目录变量是只对特定目录及子目录有效的 shell 环境变量，主要使用场景是根据目录变量来指定版本的 python 和 golang。

先安装 direnv 命令 =brew install direnv=, 然后在 =~/.bashrc= 中添加:
#+begin_src bash :tangle no
eval "$(direnv hook bash)"
#+end_src

安装 emacs envrc 软件包，它调用 direnv 命令获取当前文件或目录的环境变量，然后更新 emacs 变量
=process-environment= 和 =exec-path= ，这样 emacs 后续启动的命令就会继承这些环境变量（direnv 包是全局的，而 envrc
是 buffer-local 的)：

#+begin_src bash :tangle ~/.emacs.d/init.sh
which direnv || brew install direnv
#+end_src

#+begin_src emacs-lisp
(use-package envrc :hook (after-init . envrc-global-mode))
#+end_src
+ C-c e a: envrc-allow
+ C-c e d: envrc-deny
+ C-c e r: envrc-reload

使用步骤：
1. 在对应目录创建 =.envrc= 文件;
2. 向 .envrc 文件添加 shell 环境变量;
3. 执行 =direnv allow .= 生效环境变量;

如果某些变量未被 lsp 识别，则需要打开 .envrc 所在目录的文件后执行 =M-x lsp-workspace-restart= 来重启 lsp。

** lsp-bridge

lsp-bridge 依赖 markdown-mode 和 python-epc(pip install epc).
#+begin_src emacs-lisp
(use-package posframe)

;; dump-jump 支持 GNU Global 的 gtags 跳转。
(use-package  dumb-jump
  :demand
  :config
  (add-hook 'xref-backend-functions #'dumb-jump-xref-activate))

;; 融合 `lsp-bridge' `find-function' 以及 `dumb-jump' 的智能跳转。
(defun lsp-bridge-jump ()
  (interactive)
  (cond
   ((eq major-mode 'emacs-lisp-mode)
    (let ((symb (function-called-at-point)))
      (when symb
        (find-function symb))))
   (lsp-bridge-mode
    (lsp-bridge-find-def))
   (t
    (require 'dumb-jump)
    (dumb-jump-go))))

(defun lsp-bridge-jump-back ()
  (interactive)
  (cond
   (lsp-bridge-mode
    (lsp-bridge-return-from-def))
   (t
    (require 'dumb-jump)
    (dumb-jump-back))))

(use-package lsp-bridge
  :after (markdown-mode)
  :straight (:host github :repo "manateelazycat/lsp-bridge" :files ("*" "acm/*"))
  :custom
  ;; 不在 modeline 显示 lsp-bridge 信息。
  (lsp-bridge-enable-mode-line nil)
  :config
  (setq lsp-bridge-enable-log t)
  (setq lsp-bridge-enable-signature-help t)
  ;;(setq lsp-bridge-signature-show-function 'lsp-bridge-signature-posframe)
  ;; word 补全。
  (setq acm-enable-search-file-words nil)
  (setq lsp-bridge-enable-search-words nil)
  (setq lsp-bridge-search-words-rebuild-cache-idle 10)
  (setq acm-candidate-match-function 'orderless-flex)
  ;; 至少输入 N 个字符后才开始补全。
  (setq acm-backend-lsp-candidate-min-length 0)
  (setq acm-backend-lsp-enable-auto-import nil)
  (setq acm-backend-lsp-candidate-max-length 100)
  (setq acm-enable-doc nil)
  (setq acm-enable-telega nil)
  (setq acm-enable-tabnine nil)
  (setq acm-enable-quick-access t)
  (setq lsp-bridge-diagnostic-tooltip-border-width 0)
  (setq lsp-bridge-enable-hover-diagnostic t)
  (setq lsp-bridge-lookup-doc-tooltip-border-width 0)
  ;;  过滤 warnning.
  (setq lsp-bridge-diagnostic-hide-severities '(2 3 4))
  ;; 开启 citre 集成。
  (setq acm-enable-citre t)
  ;; 关闭 yas 补全。
  (setq acm-enable-yas nil)
  ;; 增加 treesit 相关的 xx-ts-mode
  (setq lsp-bridge-single-lang-server-mode-list
        '(((c-mode c-ts-mode c++-mode c++-ts-mode objc-mode) . lsp-bridge-c-lsp-server)
          (cmake-mode . "cmake-language-server")
          ((java-mode java-ts-mode) . "jdtls")
          ((python-mode python-ts-mode) . lsp-bridge-python-lsp-server)
          ((go-mode go-ts-mode) . "gopls")
          ((js2-mode js-mode js-ts-mode rjsx-mode) . "javascript")
          (typescript-tsx-mode . "typescriptreact")
          ((typescript-mode typescript-ts-mode) . "typescript")
          ((latex-mode Tex-latex-mode texmode context-mode texinfo-mode bibtex-mode) . lsp-bridge-tex-lsp-server)
          ((sh-mode bash-ts-mode) . "bash-language-server")
          ((css-mode css-ts-mode) . "vscode-css-language-server")
          ((yaml-mode yaml-ts-mode) . "yaml-language-server")
          ((json-mode json-ts-mode) . "vscode-json-language-server")
          ((dockerfile-mode dockerfile-ts-mode) . "docker-langserver")))
  ;; 添加 treesit 相关的 xx-ts-mode-hook 后，lsp-bridge 才会自动启动。
  (add-to-list 'lsp-bridge-default-mode-hooks 'go-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'bash-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'python-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'js-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'typescript-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'css-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'yaml-ts-mode-hook)
  (add-to-list 'lsp-bridge-default-mode-hooks 'json-ts-mode-hook)
  (add-to-list 'lsp-bridge-org-babel-lang-list "emacs-lisp")
  (add-to-list 'lsp-bridge-org-babel-lang-list "sh")
  (add-to-list 'lsp-bridge-org-babel-lang-list "shell")
  (add-to-list 'lsp-bridge-org-babel-lang-list "go")
  ;; go 缩进。
  (add-to-list 'lsp-bridge-formatting-indent-alist '(go-mode . c-basic-offset))
  (add-to-list 'lsp-bridge-formatting-indent-alist '(go-ts-mode . c-basic-offset))
  ;; go 注释字符后不提示补全。
  (add-to-list 'lsp-bridge-completion-hide-characters "/")
  (add-to-list 'lsp-bridge-completion-hide-characters "，")
  (add-to-list 'lsp-bridge-completion-hide-characters "。")
  (global-lsp-bridge-mode)
  ;;; lsp-bridge
  ;; M-j 被预留给 pyim 使用。
  (define-key acm-mode-map (kbd "M-j") nil)
  ;; 使用 TAB 而非回车键来选定。
  ;;(define-key acm-mode-map (kbd "RET") nil)
  (define-key lsp-bridge-mode-map (kbd "M-.") #'lsp-bridge-find-def)
  (define-key lsp-bridge-mode-map (kbd "C-M-.") #'lsp-bridge-find-def-other-window)
  (define-key lsp-bridge-mode-map (kbd "M-,") #'lsp-bridge-find-def-return)
  (define-key lsp-bridge-mode-map (kbd "M-?") #'lsp-bridge-find-references)
  (define-key lsp-bridge-mode-map (kbd "M-d") #'lsp-bridge-popup-documentation)
  ;; 这两个快捷键让位于 symobl-overlay
  ;; (define-key lsp-bridge-mode-map (kbd "M-n") #'lsp-bridge-popup-documentation-scroll-up)
  ;; (define-key lsp-bridge-mode-map (kbd "M-p") #'lsp-bridge-popup-documentation-scroll-down)
  (define-key lsp-bridge-mode-map (kbd "C-c C-a") #'lsp-bridge-code-action)
  (define-key lsp-bridge-mode-map (kbd "C-c C-f") #'lsp-bridge-code-format)
  (define-key lsp-bridge-mode-map (kbd "C-s-l") #'lsp-bridge-diagnostic-list)
  (define-key lsp-bridge-mode-map (kbd "C-s-n") #'lsp-bridge-diagnostic-jump-next)
  (define-key lsp-bridge-mode-map (kbd "C-s-p") #'lsp-bridge-diagnostic-jump-prev))
#+end_src
+ acm-backend-search-words, 用来替换 acm-backend-dabbrev, acm-backend-search-words 后端的原理是， Emacs 告诉
  Python 进程哪些文件被打开了， Python 进程就开一个子线程在后台计算每个文件的单词， 并对提取的单词进行清洗、去
  重和排序。
+ M-x lsp-bridge-toggle-english-helper: 快速切换英文单词补全后端；
  
** python
*** pyenv
=pyenv= 和 =pyenv-virtualenv= 可以为项目或系统指定不同隔离的 python 或 venv 版本。
#+begin_src bash :tangle ~/.emacs.d/init.sh
which pyenv || brew install --HEAD pyenv
which pyenv-virtualenv || brew install --HEAD pyenv-virtualenv
#+end_src

为了在进入项目目录时自动切换到指定 pyenv 或 venv 版本，需要配置 =~/.bashrc= ：
#+begin_src bash :results none
eval "$(pyenv init -)"
eval "$(pyenv virtualenv-init -)"
eval "$(jenv init -)"
#+end_src

pyenv 工作流：
1. 列出可以安装的 python 版本： =pyenv install -l=
2. 安装指定的 python 版本： =pyenv install <version>=
3. 创建一个 pyenv virtualenv： =pyenv virtualenv [version] <virtualenv-name>=
4. 为项目指定 python 版本或上一步创建的 virtualenv 名称：
   + 在项目根目录执行 =pyenv local <version1> <version2>=
5. 指定用户默认使用 3.9.0 python 版本:
   + 在家目录执行命令: cd ~ && pyenv local 3.9.0
   + 后续家目录下执行 pip 命令安装的包都是 3.x 版本。
6. 如果虚拟环境中没有 pip 命令，安装： =python -m ensurepip=

*** python-mode

#+begin_src bash :tangle ~/.emacs.d/init.sh
which pylint || brew install pylint
which flake8 || brew install flake8
which pyright || npm update -g pyright
which yapf || pip install yapf
which ipython || pip install ipython
#+end_src

#+begin_src emacs-lisp
(defun my/python-setup-shell (&rest args)
  (if (executable-find "ipython")
      (progn
        (setq python-shell-interpreter "ipython")
        (setq python-shell-interpreter-args "--simple-prompt -i"))
    (progn
      (setq python-shell-interpreter "python")
      (setq python-shell-interpreter-args "-i"))))

;; 使用 yapf 格式化 python 代码。
(use-package yapfify :straight (:host github :repo "JorisE/yapfify"))

(use-package python
  :defer
  :init
  (defvar pyright-directory "~/.emacs.d/.cache/lsp/npm/pyright/lib")
  (if (not (file-exists-p pyright-directory))
      (make-directory pyright-directory t))
  (setq python-indent-guess-indent-offset t)  
  (setq python-indent-guess-indent-offset-verbose nil)
  (setq python-indent-offset 2)
  ;;(with-eval-after-load 'exec-path-from-shell (exec-path-from-shell-copy-env "PYTHONPATH"))
  :hook
  (python-mode . (lambda ()
                   (my/python-setup-shell)
                   (yapf-mode))))
#+end_src
+ 需要在对应的 python env 中安装 pylint/flake8/yapf 程序。

*** pyright

微软不再维护 python-language-server，主力发展 pyright 和 pyglance，所以不再使用 lsp-python-ms 和 pyls，而使用
lsp-pyright。
+ python-lanuage-server 的活跃 fork 版本: https://github.com/python-lsp/python-lsp-server
+ lsp-pyright 是 lsp-mode 的 pyright emacs client, 在使用 lsp-bridge 后，只需要安装 pyright npm 包即可，不需要
  再安装 lsp-pyright.

pyright _不使用_ pyenv ~.python-version~ 指定的 python 版本或 venv 来搜索依赖的 module，而是使用
=pyrightconfig.json= 文件中配置的 venv 和 venvPath:
+ venvPath：指定查找 venv 目录的上级目录，可以包含多个 venv 环境；
+ venv：指定 venvPath 目录下的、使用的虚拟环境名称, pyright 在该 venv 中搜索依赖的 package;

安装 =pyenv-pyright= 插件来方便的创建和更新 =pyrightconfig.json= 文件：
#+begin_src bash :results none
git clone https://github.com/alefpereira/pyenv-pyright.git $(pyenv root)/plugins/pyenv-pyright
#+end_src

使用方法：
1. 使用 =pyenv local= 为项目指定 ~pyenv virtualenv~;
2. 使用 =pyenv pyright= 来自动配置 =pyrightconfig.json= 使用上一步指定的 virtualenv；

pyright 假设源文件位于项目 scr 目录下，但实际可能会在多个其它子目录（甚至嵌套情况）中放置项目源码，即
=multi-root= 模式（对应于 vscode 中的多 worksapce 目录)，这时可能出现大量 import 错误，可以通过在项目根目录配置
=pyrightconfig.json= 文件来解决，例如（参考：python module [[https://github.com/microsoft/pyright/blob/main/docs/import-resolution.md][Import Resolution]]）：
#+begin_src javascript :tangle no
{
    "venv": "venv-2.7.18",
    "venvPath": "/Users/zhangjun/.pyenv/versions",
    "verboseOutput": true,
    "reportMissingTypeStubs": false,
    "executionEnvironments": [
        {
            "root": "scripts",
            "extraPaths": [
                ".",  // scripts 目录下 py 文件导入同级 py 文件的情况
                "scripts/appinstance_apply"
            ]
        }
    ]
}
#+end_src

executionEnvironments：
1. 列表中 root 指定各 workspace 的子目录，是有搜索优先级的，所以如果有相同路径前缀的情况，应该从长到短依列出来：
   根据 python 文件的 from/import 语句来确定root 路径：即从项目根目录（pyrightconfig.json 文件所在目录）开始到
   文件中导入路径最开始所在目录之间的目录，都应该是 root。
2. extraPaths 列表中的路径可以是绝对路径或相对路径（相对于 pyrightconfig.json 文件），用于添加额外的 python
   module 搜索路径；
   + 添加 "." 是因为需要将 scripts 所在的目录也添加到 module 搜索路径，而不仅仅是 scripts 下的子目录；
3. 官方的实例参考：[[https://github.com/microsoft/pyright/blob/main/docs/configuration.md#sample-config-file][Sample Config File]] 和 [[https://github.com/microsoft/pyright/blob/main/packages/pyright-internal/src/tests/testState.test.ts][testState.test.ts]]；

[[https://github.com/Microsoft/pyright/issues/21][pyright 不支持 python 2.x]]，如果在上面文件配置 ="pythonVersion": "2.7"= 则会报错。

修改 pyrightconfig.json 后，需要执行 ~M-x lsp-workspace-restart~ 来重启 lsp，如果还是有问题，则可以查看
=*lsp-log*= buffer 的日志。

** go
#+begin_src bash :tangle ~/.emacs.d/init.sh
which gopls || go install golang.org/x/tools/gopls@latest
#+end_src

#+begin_src emacs-lisp
(defun my/go-setup ()
    (setenv "GOOS" "linux")
    (setenv "GOARCH" "amd64")
     ;; go-mode 默认启用 tabs.
    (setq indent-tabs-mode t)
    (setq c-ts-common-indent-offset 8)
    (setq c-basic-offset 8))

(use-package go-mode
  :init
  (setq godoc-reuse-buffer t))
(add-hook 'go-mode-hook 'my/go-setup)
(add-hook 'go-ts-mode-hook 'my/go-setup)
#+end_src
+ godoc-gogetdoc 调用外部命令 gogetdoc 来获取文档，速度慢，而且不显示类型的方法，不好用！
+ lsp-bridge 支持 format 和 autoimport;

修改 gopls 配置：
+ 添加 ~"verboseOutput": true~ 配置参数将打开 gopls 调试模式；
+ 去掉配置参数 ~"env": {"GOFLAGS": "-mod=mod"},~ 可以防止带有 vendor 目录的项目报错: go: inconsistent vendoring;
  + 项目如果要使用 vendor, 则可以使用 .envrc 添加项目相关的环境变量："export GOFLAGS='-mod=vendor'"
+ usePlaceholders 依赖于 yasnippet, 使用时容易出错，故关闭。lsp-bridge 在 echo area 提供了 signature-help 的功
  能，也能方便的看到函数签名。
+ json 配置必须保存到 straight/repos 目录下（而非 build 目录）下才生效。
#+begin_src javascript :tangle ~/.emacs.d/straight/repos/lsp-bridge/langserver/gopls.json
{
  "name": "gopls",
  "languageId": "go",
  "command": ["gopls", "-remote=auto"],
  "settings":{
    "gopls": {
      "usePlaceholders": false,
      "completeUnimported": true,
      "staticcheck": true,
      "allowModfileModifications": true,
      "allowImplicitNetworkAccess": true
    }
  }
}
#+end_src

如果一个 git 项目下有多个 go module, 则需要在上层目录创建 workspace, 并将各 module 加入其中，否则可能出现
package import 失败的情况：
#+begin_src bash :tangle no
go work init
go work use ./path/to/module1 ./path/to/module2
#+end_src
  
安装或更新工具：
#+begin_src emacs-lisp
(defvar go--tools '("golang.org/x/tools/gopls"
                    "golang.org/x/tools/cmd/goimports"
                    "honnef.co/go/tools/cmd/staticcheck"
                    "github.com/go-delve/delve/cmd/dlv"
                    "github.com/zmb3/gogetdoc"
                    "github.com/josharian/impl"
                    "github.com/cweill/gotests/..."
                    "github.com/fatih/gomodifytags"
                    "github.com/davidrjenni/reftools/cmd/fillstruct"))

(defun go-update-tools ()
  (interactive)
  (unless (executable-find "go")
    (user-error "Unable to find `go' in `exec-path'!"))
  (message "Installing go tools...")
  (dolist (pkg go--tools)
    (set-process-sentinel
     (start-process "go-tools" "*Go Tools*" "go" "install" "-v" "-x" (concat pkg "@latest"))
     (lambda (proc _)))))

(use-package go-fill-struct)
(use-package go-impl)
(use-package go-tag
  :init
  (setq go-tag-args (list "-transform" "camelcase"))
  :config
  (define-key go-mode-map (kbd "C-c t a") #'go-tag-add)
  (define-key go-mode-map (kbd "C-c t r") #'go-tag-remove))
(use-package go-playground :commands (go-playground-mode))
#+end_src

** markdown

#+begin_src bash :tangle ~/.emacs.d/init.sh
which multimarkdown || brew install multimarkdown
which grip || pip install grip
#+end_src

multimarkdown 将 markdown 转换为 html 进行 preview，可以结合 xwidget webkit 或 grip 进行实时预览：
#+begin_src emacs-lisp
(use-package markdown-mode
  :commands (markdown-mode gfm-mode)
  :mode
  (("README\\.md\\'" . gfm-mode) ;; gfm: github flavored markdown.
   ("\\.md\\'" . markdown-mode)
   ("\\.markdown\\'" . markdown-mode))
  :init
  (when (executable-find "multimarkdown")
    (setq markdown-command "multimarkdown"))
  (setq markdown-enable-wiki-links t)
  (setq markdown-italic-underscore t)
  (setq markdown-asymmetric-header t)
  (setq markdown-make-gfm-checkboxes-buttons t)
  (setq markdown-gfm-uppercase-checkbox t)
  (setq markdown-fontify-code-blocks-natively t)
  (setq markdown-gfm-additional-languages "Mermaid")
  (setq markdown-content-type "application/xhtml+xml")
  (setq markdown-css-paths '("https://cdn.jsdelivr.net/npm/github-markdown-css/github-markdown.min.css"
                             "https://cdn.jsdelivr.net/gh/highlightjs/cdn-release/build/styles/github.min.css"))
  (setq markdown-xhtml-header-content "
<meta name='viewport' content='width=device-width, initial-scale=1, shrink-to-fit=no'>
<style>
body {
  box-sizing: border-box;
  max-width: 740px;
  width: 100%;
  margin: 40px auto;
  padding: 0 10px;
}
</style>
<link rel='stylesheet' href='https://cdn.jsdelivr.net/gh/highlightjs/cdn-release/build/styles/default.min.css'>
<script src='https://cdn.jsdelivr.net/gh/highlightjs/cdn-release/build/highlight.min.js'></script>
<script>
document.addEventListener('DOMContentLoaded', () => {
  document.body.classList.add('markdown-body');
  document.querySelectorAll('pre code').forEach((code) => {
    if (code.className != 'mermaid') {
      hljs.highlightBlock(code);
    }
  });
});
</script>
<script src='https://unpkg.com/mermaid@8.4.8/dist/mermaid.min.js'></script>
<script>
mermaid.initialize({
  theme: 'default',  // default, forest, dark, neutral
  startOnLoad: true
});
</script>
"))
#+end_src

使用 grip 来预览 markdown 文件，它调用 github markdown API 来渲染文件，从而确保渲染后分隔和 Github 一致。为了
避免 API 调用频率限制，可以创建一个空 scop 的 Access Token，然后将 username 和 token 保存到 =~/.authinfo.gpg= 文
件中：

#+begin_src bash :tangle no
machine api.github.com login geekard@qq.com password YOUR_TOKEN
#+end_src

在 Markdown Buffer 中，执行 =M-x grip-mode= 来启用实时预览，然后可以执行如下命令：
+ M-x grip-start-preview
+ M-x grip-stop-preview
+ M-x grip-restart-preview
+ M-x grip-browse-preview 使用浏览器来预览
#+begin_src emacs-lisp
(use-package grip-mode
  :after (markdown-mode)
  :config
  (setq grip-preview-use-webkit nil)
  ;; 支持网络访问（默认 localhost）。
  (setq grip-preview-host "0.0.0.0")
  ;; 保存文件时才更新预览。
  (setq grip-update-after-change nil)
  ;; 从 ~/.authinfo 文件获取认证信息。
  (require 'auth-source)
  (let ((credential (auth-source-user-and-password "api.github.com")))
    (setq grip-github-user (car credential)
          grip-github-password (cadr credential)))
  ;;; markdown grip-mode
  (define-key markdown-mode-command-map (kbd "g") #'grip-mode))

#+end_src

为 markdown 文件添加目录：
#+begin_src emacs-lisp
(use-package markdown-toc
  :after(markdown-mode)
  :config
  (define-key markdown-mode-command-map (kbd "r") #'markdown-toc-generate-or-refresh-toc))
#+end_src

** web

#+begin_src bash :tangle ~/.emacs.d/init.sh
which tsc || npm install -g typescript
which typescript-language-server  || npm install -g typescript-language-server
which eslint || npm install -g eslint babel-eslint eslint-plugin-react
which prettier || npm install -g prettier
which importjs || npm install -g import-js
which yaml-language-server || npm install -g yaml-language-server
which vscode-css-language-server &>/dev/null || npm i -g vscode-langservers-extracted
#+end_src

*** typescript
typescript-mode 为 typescript 文件（扩展名为 .ts 和 .tsx) 提供编辑支持（major-mode)。 js-mode/js2-mode 则
为.js/.jsx 文件提供编辑支持。
#+begin_src emacs-lisp
;; for .ts/.tsx file
(use-package typescript-mode
  :mode "\\.tsx?\\'"
  :config
  (setq typescript-indent-level 2))
#+end_src

在安装 typescript-mode 包的同时，确保已安装 typescript 和 typescript-language-server 包：
+ =npm install -g typescript=: 提供 tsc 和 tsserver 命令。
+ =npm install -g typescript-language-server=: 基于 typescript 的 tsserver 实现的语言服务器, 支持以下三种语言的
  补全：
  + typescript: 扩展名 .ts/.tsx
  + javascript: 扩展名 .js/.jsx
  + 其中 .tsx/.jsx 是 React 的语法格式。

eslint:
+ 安装 eslint npm 包后，安装语言服务器 =M-x lsp-install-server RET eslint RET= 。
+ 创建 .eslintrc.js: =M-x lsp-eslint-create-default-configuration= , 回答一些问题后自动创建配置文件并安装 eslint
plugin。

prettier 提供了 javascript/typescript 的格式化的功能。import-js 则提供了 import 功能。

*** js

Emacs 27 内置的 js-mode 完整支持 .js/.jsx 文件的编辑, [[https://github.com/mooz/js2-mode#react-and-jsx][官方建议]]将 js2 作为 js-mode 的 minor-mode 来一起使用，这
样 js2 为 js-mode 提供了更好的 AST 和 JavaScript linting 支持能力。
#+begin_src emacs-lisp
(use-package js2-mode
  :init
  (add-to-list 'auto-mode-alist '("\\.jsx?\\'" . js-mode))
  :config
  ;; 仍然使用 js-mode 作为 .js/.jsx 的 marjor-mode, 但使用 js2-minor-mode 提供 AST 解析。
  (add-hook 'js-mode-hook 'js2-minor-mode)
  ;; 将 js2-mode 作为 .js/.jsx 的 major-mode
  ;;(add-to-list 'auto-mode-alist '("\\.js\\'" . js2-mode))
  ;; 由于 lsp 已经提供了 diagnose 功能，故关闭 js2 自带的错误检查，防止干扰。
  (setq js2-mode-show-strict-warnings nil)
  (setq js2-mode-show-parse-errors nil)
  ;; 缩进配置。
  (setq javascript-indent-level 2)
  (setq js-indent-level 2)
  (setq js2-basic-offset 2)
  (add-to-list 'interpreter-mode-alist '("node" . js2-mode)))

;; json mode。
(use-package json-mode :straight t :defer t)
#+end_src

*** web-mode

web-mode 用于编辑 html/css/jinja2/gotmpl/tmpl 等模板文件，不用于编辑 js/jsx/ts/tsx 等类型文件。
#+begin_src  emacs-lisp
(use-package web-mode
  :mode "(\\.\\(jinja2\\|j2\\|css\\|vue\\|tmpl\\|gotmpl\\|html?\\|ejs\\)\\'"
  :custom
  (css-indent-offset 2)
  (web-mode-attr-indent-offset 2)
  (web-mode-attr-value-indent-offset 2)
  (web-mode-code-indent-offset 2)
  (web-mode-css-indent-offset 2)
  (web-mode-markup-indent-offset 2)
  (web-mode-sql-indent-offset 2)
  (web-mode-enable-auto-pairing t)
  (web-mode-enable-css-colorization t)
  (web-mode-enable-auto-quoting nil)
  (web-mode-enable-block-face t)
  (web-mode-enable-current-element-highlight t)
  :config
  ;; Emmit.
  (setq web-mode-tag-auto-close-style 2) ;; 2 mean auto-close with > and </.
  (setq web-mode-markup-indent-offset 2))
#+end_src

*** css

vscode-langservers-extracted 提供了如下三个 web 开发相关的 lsp server:
+ vscode-html-language-server
+ vscode-css-language-server
+ vscode-json-language-server
+ vscode-eslint-language-server

lsp-bridge 默认在打开 css-mode 时使用 vscode-css-language-server。
+ 各语言使用的 language server 参考变量: lsp-bridge-lang-server-mode-list
  
** yaml

#+begin_src  emacs-lisp
(use-package yaml-mode
  :mode "\\.ya?ml\\'"
  :config
  (define-key yaml-mode-map (kbd "\C-m") #'newline-and-indent))
#+end_src

** shell

Emacs 通过名为 =Shell-script(sh-mode)= 的 major mode 来支持编辑 shell 脚本，由于各种 shell 脚本语法类似，所以
sh-mode 其实也支持多种 shell 类型。

安装 bash language server:
#+begin_src bash :tangle ~/.emacs.d/init.sh
bash-language-server -v &>/dev/null || npm i -g bash-language-serverw
#+end_src

安装 Shell 脚本的静态分析工具 ShellCheck:
#+begin_src bash ~/.emacs.d/init.sh
shellcheck -V &>/dev/null || brew install shellcheck
#+end_src

bash language server 使用 shellcheck 做语法检查和静态分析，使用 lsp diagnose 机制来提示错误（不需要再安装
flymake/flycheck)。

#+begin_src emacs-lisp
(setq sh-basic-offset 2)
(setq sh-indentation 2)
#+end_src

** treesit

#+begin_src emacs-lisp
;; Tree-sitter support
;; https://github.com/seagle0128/.emacs.d/blob/master/lisp/init-prog.el
;; @see https://github.com/casouri/tree-sitter-module
;;      https://git.savannah.gnu.org/cgit/emacs.git/tree/admin/notes/tree-sitter/starter-guide?h=feature/tree-sitter
(use-package treesit
  :straight (:type built-in)
  :when (and (fboundp 'treesit-available-p)
             (treesit-available-p))
  ;; :custom (major-mode-remap-alist
  ;;          '((c-mode          . c-ts-mode)
  ;;            (c++-mode        . c++-ts-mode)
  ;;            (cmake-mode      . cmake-ts-mode)
  ;;            (conf-toml-mode  . toml-ts-mode)
  ;;            (css-mode        . css-ts-mode)
  ;;            (dockerfile-mode . dockerfile-ts-mode)
  ;;            (go-mode         . go-ts-mode)
  ;;            (java-mode       . java-ts-mode)
  ;;            (json-mode       . json-ts-mode)
  ;;            (js-json-mode    . json-ts-mode)
  ;;            (js-mode         . js-ts-mode)
  ;;            (python-mode     . python-ts-mode)
  ;;            (rust-mode       . rust-ts-mode)
  ;;            (sh-mode         . bash-ts-mode)
  ;;            (typescript-mode . typescript-ts-mode)))
  ;; :config
  ;; (add-to-list 'auto-mode-alist '("\\(?:CMakeLists\\.txt\\|\\.cmake\\)\\'" . cmake-ts-mode))
  ;; (add-to-list 'auto-mode-alist '("\\.rs\\'" . rust-ts-mode))
  ;; (add-to-list 'auto-mode-alist '("\\.go\\'" . go-ts-mode))
  ;; (add-to-list 'auto-mode-alist '("\\.py\\'" . python-ts-mode))
  ;; (add-to-list 'auto-mode-alist '("\\.ts\\'" . typescript-ts-mode))
  ;; (add-to-list 'auto-mode-alist '("\\.tsx\\'" . tsx-ts-mode))
  ;; (add-to-list 'auto-mode-alist '("\\.ya?ml\\'" . yaml-ts-mode))
  )

(use-package treesit-auto
  :straight (treesit-auto :type git :host github :repo "renzmann/treesit-auto")
  :demand t
  :config
  (setq treesit-auto t)
  (global-treesit-auto-mode))
#+end_src

** ts-fold

高性能的, 使用 tree-sitter 进行展开和隐藏。
+ 依赖于非内置的 elisp-tree-sitter, 暂时禁用。
#+begin_src emacs-lisp
(use-package ts-movement
  :straight (ts-movement :type git :host github :repo "haritkapadia/ts-movement")
  :hook
  (bash-ts-mode-hook . ts-movement-mode)
  (c++-ts-mode-hook . ts-movement-mode)
  (c-ts-mode-hook . ts-movement-mode)
  (cmake-ts-mode-hook . ts-movement-mode)
  (csharp-ts-mode-hook . ts-movement-mode)
  (css-ts-mode-hook . ts-movement-mode)
  (dockerfile-ts-mode-hook . ts-movement-mode)
  (go-ts-mode-hook . ts-movement-mode)
  (java-ts-mode-hook . ts-movement-mode)
  (js-ts-mode-hook . ts-movement-mode)
  (json-ts-mode-hook . ts-movement-mode)
  (python-ts-mode-hook . ts-movement-mode)
  (ruby-ts-mode-hook . ts-movement-mode)
  (rust-ts-mode-hook . ts-movement-mode)
  (toml-ts-mode-hook . ts-movement-mode)
  (tsx-ts-mode-hook . ts-movement-mode)
  (typescript-ts-mode-hook . ts-movement-mode)
  (yaml-ts-mode-hook . ts-movement-mode))

(use-package ts-fold
  :straight (ts-fold :type git :host github :repo "emacs-tree-sitter/ts-fold")
  :disabled
  :config
  (global-ts-fold-mode)
  (global-set-key (kbd "C-c C-<tab>") 'ts-fold-toggle)
  ;; indicators 影响性能；
  ;; (add-hook 'tree-sitter-after-on-hook #'ts-fold-indicators-mode)
  )
#+end_src

** citre

安装 GNU global 和 pygments,  global 依赖 universal-ctags, 通过 pygments 能生成更丰富的 TAG 内容，同时支持
reference 搜索。
+ https://github.com/universal-ctags/citre/blob/master/docs/user-manual/citre-global.md 
#+begin_src bash :tangle ~/.emacs.d/init.sh
pip install pygments
python -m pygments -h # gtags 使用 pygments 支持跟多语言

brew install global
# 统一的 tags 文件目录
export GTAGSOBJDIRPREFIX=~/.cache/gtags/ 
mkdir $GTAGSOBJDIRPREFIX
export GTAGSCONF=/usr/local/Cellar/global/6.6.9/share/gtags/gtags.conf
# 使用 pygments 支持更多的语言，他噢夹南是支持 reference 搜索。
export GTAGSLABEL=pygments

# 测试项目
cd go/src/github.com/docker/swarm/
# 生成 TAGS 文件
gtags --explain
# reference
global -xr SetPrimary
# definition
global -x SetPrimary
#+end_src

citre 是基于 TAGS 文件的代码浏览工具， 支持[[https://github.com/universal-ctags/citre/blob/master/docs/user-manual/citre-global.md][集成使用 GNU global TAGS 文件]]：
#+begin_src emacs-lisp
;; GNU Global gtags
(setenv "GTAGSOBJDIRPREFIX" (expand-file-name "~/.cache/gtags/"))
(setenv "GTAGSCONF" "/usr/local/Cellar/global/6.6.9/share/gtags/gtags.conf")
(setenv "GTAGSLABEL" "pygments")

(use-package citre
  :defer t
  :straight (:host github :repo "universal-ctags/citre")
  :init
  ;; 当项目目录有 TAGS 文件时，自动开启 citre-mode.
  (require 'citre-config)
  (setq citre-peek-file-content-height 20)
  (global-set-key (kbd "s-.") 'citre-jump)
  (global-set-key (kbd "s-,") 'citre-jump-back)
  (global-set-key (kbd "s-?") 'citre-peek) ;; or citre-ace-peek
  (global-set-key (kbd "C-x c u") 'citre-update-this-tags-file))
#+end_src

创建 Global TAG 数据库文件（~/.cache/gtags/)：
+ M-x citre-global-update-database
+ M-x citre-global-create-database

使用 citre: 
+ M-x citre-jump-to-reference, which reuses the citre-jump UI;
+ M-x citre-peek-references, equivalent to citre-peek;
+ M-x citre-ace-peek-references, equivalent to citre-ace-peek;
+ M-x citre-peek-through-references, equivalent to citre-peek-through.

citre 默认支持 imenu 和 xref 集成（通过向 xref-backend-functions hook 添加 citre-xref-backend）， 所以可以使用
~M-.~ 和 ~M-,~ 跳转, 也可以使用 consult-imenu 展示当前 buffer 的条目。

在 citre-jump 的弹出时 buffer 中可以使用正则语法对候选者进行过滤, 例如:
: something kind:^member$ kind:^macro$ input:.c$

** compile

#+begin_src emacs-lisp
;; Don't ask before killing the current compilation. This is useful if
;; you're running servers after compiling them, so that the compilation never finishes.
(setq compilation-ask-about-save nil
      compilation-always-kill t
      compile-command "go build")
;; Convert shell escapes to color
(add-hook 'compilation-filter-hook
          (lambda () (ansi-color-apply-on-region (point-min) (point-max))))

;; Taken from https://emacs.stackexchange.com/questions/31493/print-elapsed-time-in-compilation-buffer/56130#56130
(make-variable-buffer-local 'my-compilation-start-time)

(add-hook 'compilation-start-hook #'my-compilation-start-hook)
(defun my-compilation-start-hook (proc)
  (setq my-compilation-start-time (current-time)))

(add-hook 'compilation-finish-functions #'my-compilation-finish-function)
(defun my-compilation-finish-function (buf why)
  (let* ((elapsed  (time-subtract nil my-compilation-start-time))
         (msg (format "Compilation took: %s" (format-time-string "%T.%N" elapsed t))))
    (save-excursion (goto-char (point-max)) (insert msg))
    (message "Compilation %s: %s" (string-trim-right why) msg)))
#+end_src

** others

#+begin_src emacs-lisp
;; 移动到行或代码的开头、结尾。
(use-package mwim
  :config
  (define-key global-map [remap move-beginning-of-line] #'mwim-beginning-of-code-or-line)
  (define-key global-map [remap move-end-of-line] #'mwim-end-of-code-or-line))

;; 开发文档。
(use-package dash-at-point
  :config
  (global-set-key (kbd "C-c d .") #'dash-at-point)
  (global-set-key (kbd "C-c d d") #'dash-at-point-with-docset))

(use-package expand-region
  :init
  (define-advice set-mark-command (:before-while (arg))
    "Repeat C-SPC to expand region."
    (interactive "P")
    (if (eq last-command 'set-mark-command)
        (progn
          (er/expand-region 1)
          nil)
      t))
  :config
  (global-set-key (kbd "C-=") #'er/expand-region))
#+end_src

** blink-search

#+begin_src emacs-lisp
(use-package blink-search
  :straight (blink-search :host github :repo "manateelazycat/blink-search" :files ("*" "backend/*" ))
  :demand
)
#+end_src

* project

#+begin_src emacs-lisp
(use-package project
  :custom
  (project-switch-commands
   '(
     (consult-project-buffer "buffer" ?b)
     (project-dired "dired" ?d)
     (magit-project-status "magit status" ?g)
     (project-find-file "find file" ?p)
     (consult-ripgrep "rigprep" ?r)
     (vterm-toggle-cd "vterm" ?t)))
  (compilation-always-kill t)
  (project-vc-merge-submodules nil)
  :config
  ;; project-find-file 忽略的目录或文件列表。
  (add-to-list 'vc-directory-exclusion-list "vendor")
  (add-to-list 'vc-directory-exclusion-list "node_modules"))

(defun my/project-try-local (dir)
  "Determine if DIR is a non-Git project."
  (catch 'ret
    (let ((pr-flags '((".project")
                      ("go.mod" "pom.xml" "package.json")
                      ;; 以下文件容易导致 project root 判断失败, 故关闭。
                      ;; ("Makefile" "README.org" "README.md")
                      )))
      (dolist (current-level pr-flags)
        (dolist (f current-level)
          (when-let ((root (locate-dominating-file dir f)))
            (throw 'ret (cons 'local root))))))))

(setq project-find-functions '(my/project-try-local project-try-vc))

(cl-defmethod project-root ((project (head local)))
  (cdr project))

(defun my/project-info ()
  (interactive)
  (message "%s" (project-current t)))

(defun my/project-add (dir)
  (interactive "DDirectory: \n")
  ;; 使用 project-remember-project 报错。
  (project-remember-projects-under dir nil))

(defun my/project-new-root ()
  (interactive)
  (let* ((root-dir (read-directory-name "Root: "))
         (f (expand-file-name ".project" root-dir)))
    (message "Create %s..." f)
    (make-directory root-dir t)
    (when (not (file-exists-p f))
      (make-empty-file f))
    (my/project-add root-dir)))

(defun my/project-discover ()
  (interactive)
  (dolist (search-path '("~/go/src/github.com/*" "~/go/src/github.com/*/*" "~/go/src/k8s.io/*" "~/go/src/gitlab.*/*/*"))
    (dolist (file (file-expand-wildcards search-path))
      (when (file-directory-p file)
          (message "dir %s" file)
          ;; project-remember-projects-under 列出 file 下的目录, 分别加到 project-list-file 中。
          (project-remember-projects-under file nil)
          (message "added project %s" file)))))

;; 不将 tramp 项目记录到 projects 文件中，防止 emacs-dashboard 启动时检查 project 卡住。
(defun my/project-remember-advice (fn pr &optional no-write)
  (let* ((remote? (file-remote-p (project-root pr)))
         (no-write (if remote? t no-write)))
    (funcall fn pr no-write)))
(advice-add 'project-remember-project :around 'my/project-remember-advice)
#+end_src

* proxy

全局 socks5 代理：
+ Mac 自带的 curl 不支持 socks 代理, 需要安装 =brew install curl= 并设置
   ~export PATH="/usr/local/opt/curl/bin:$PATH"~
+ [[https://emacstalk.github.io/post/007/][url-retrieve 使用 curl 作为后端实现]], 这样全局可使用 socks5 代理。
+ 需要添加 =--user-agent= 配置, 否则会被 Google 403 Forbidden;

#+begin_src bash :tangle  ~/.emacs.d/init.sh
ls -l /usr/local/opt/curl/bin/curl || brew install curl
export PATH="/usr/local/opt/curl/bin:$PATH"
#+end_src

#+begin_src emacs-lisp
;; 添加环境变量 
(setq my/socks-host "127.0.0.1")
(setq my/socks-port 13659)
(setq my/socks-proxy (format "socks5h://%s:%d" my/socks-host my/socks-port))

(use-package mb-url-http
  :demand
  :straight (mb-url :repo "dochang/mb-url")
  :commands (mb-url-http-around-advice)
  :init
  (require 'auth-source)
  (let ((credential (auth-source-user-and-password "api.github.com")))
    (setq github-user (car credential)
          github-password (cadr credential))
    (setq github-auth (concat github-user ":" github-password))
    (setq mb-url-http-backend 'mb-url-http-curl
          mb-url-http-curl-program "/usr/local/opt/curl/bin/curl"
          mb-url-http-curl-switches `("-k" "-x" ,my/socks-proxy
                                      ;;"--max-time" "300"
                                      ;;"-u" ,github-auth
                                      ;;"--user-agent" "User-Agent: Mozilla/5.0 (Macintosh; Intel Mac OS X 10_15_7) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/94.0.4606.71 Safari/537.36"
                                      ))))

(defun proxy-socks-show ()
  "Show SOCKS proxy."
  (interactive)
  (when (fboundp 'cadddr)
    (if (bound-and-true-p socks-noproxy)
        (message "Current SOCKS%d proxy is %s:%d" 5 my/socks-host my/socks-port)
      (message "No SOCKS proxy"))))

(defun proxy-socks-enable ()
  "使用 socks 代理 url 访问请求。"
  (interactive)
  (require 'socks)
  (setq url-gateway-method 'socks
        socks-noproxy '("0.0.0.0" "localhost" "10.0.0.0/8" "172.0.0.0/8" "*cn" "*alibaba-inc.com" "*taobao.com" "*antfin-inc.com")
        socks-server `("Default server" ,my/socks-host ,my/socks-port 5))
  (setenv "all_proxy" my/socks-proxy)
  (setenv "ALL_PROXY" my/socks-proxy)
  (setenv "HTTP_PROXY" nil)
  (setenv "HTTPS_PROXY" nil)
  (proxy-socks-show)
  ;;url-retrieve 使用 curl 作为后端实现, 支持全局 socks5 代理。
  (advice-add 'url-http :around 'mb-url-http-around-advice))

(defun proxy-socks-disable ()
  "Disable SOCKS proxy."
  (interactive)
  (require 'socks)
  (setq url-gateway-method 'native
        socks-noproxy nil)
  (setenv "all_proxy" "")
  (setenv "ALL_PROXY" "")
  (proxy-socks-show))

(defun proxy-socks-toggle ()
  "Toggle SOCKS proxy."
  (interactive)
  (require 'socks)
  (if (bound-and-true-p socks-noproxy)
      (proxy-socks-disable)
    (proxy-socks-enable)))
#+end_src

* terminal

#+begin_src bash :tangle ~/.emacs.d/init.sh
which cmake || brew install cmake
which glibtool || brew install libtool
which exiftran || brew install fxiftran
#+end_src


#+begin_src emacs-lisp
(use-package vterm
  :hook
  ;; vterm buffer 使用 fixed pitch 的 mono 字体，否则部分终端表格之类的程序会对不齐。
  (vterm-mode . (lambda ()
                  (set (make-local-variable 'buffer-face-mode-face) 'fixed-pitch)
                  (buffer-face-mode t)))
  :config
  (setq vterm-set-bold-hightbright t)
  (setq vterm-always-compile-module t)
  (setq vterm-max-scrollback 100000)
  (add-to-list 'vterm-tramp-shells '("ssh" "/bin/bash"))
  ;; vterm buffer 名称，需要配置 shell 来支持（如 bash 的 PROMPT_COMMAND）。
  (setq vterm-buffer-name-string "*vterm: %s")
  (add-hook 'vterm-mode-hook
            (lambda ()
              (setf truncate-lines nil)
              (setq-local show-paren-mode nil)
              (setq-local global-hl-line-mode nil)
              (yas-minor-mode -1)))
  ;; 使用 M-y(consult-yank-pop) 粘贴剪贴板历史中的内容。
  (define-key vterm-mode-map [remap consult-yank-pop] #'vterm-yank-pop)
  (define-key vterm-mode-map (kbd "C-l") nil)
  ;; 防止输入法切换冲突。
  (define-key vterm-mode-map (kbd "C-\\") nil))

(use-package multi-vterm
  :after (vterm)
  :config
  (define-key vterm-mode-map  [(control return)] #'multi-vterm))

(use-package vterm-toggle
  :after (vterm)
  :custom
  ;; 由于 TRAMP 模式下关闭了 projectile，scope 不能设置为 'project。
  ;;(vterm-toggle-scope 'dedicated)
  (vterm-toggle-scope 'project)
  :config
  (global-set-key (kbd "C-`") 'vterm-toggle)
  (global-set-key (kbd "C-M-`") 'vterm-toggle-cd)
  (define-key vterm-mode-map (kbd "M-RET") #'vterm-toggle-insert-cd)
  ;; 切换到一个空闲的 vterm buffer 并插入一个 cd 命令， 或者创建一个新的 vterm buffer 。
  (define-key vterm-mode-map (kbd "s-i") 'vterm-toggle-cd-show)
  (define-key vterm-mode-map (kbd "s-n") 'vterm-toggle-forward)
  (define-key vterm-mode-map (kbd "s-p") 'vterm-toggle-backward)
  (define-key vterm-copy-mode-map (kbd "s-i") 'vterm-toggle-cd-show)
  (define-key vterm-copy-mode-map (kbd "s-n") 'vterm-toggle-forward)
  (define-key vterm-copy-mode-map (kbd "s-p") 'vterm-toggle-backward))

(use-package vterm-extra
  :straight (:host github :repo "Sbozzolo/vterm-extra")
  :config
  ;;(advice-add #'vterm-extra-edit-done :after #'winner-undo)
  (define-key vterm-mode-map (kbd "C-c C-e") #'vterm-extra-edit-command-in-new-buffer))

;; 在 $HOME 目录打开一个本地 vterm buffer.
(defun my/vterm()
  "my vterm buff."
  (interactive)
  (let ((default-directory "~/")) (vterm)))
#+end_src
+ vterm-extra 提供了 vterm buffer 命令行编辑的能力，结束后按 ~C-c C-c~ 自动粘贴到对应的 vterm 中，与
  consult-yasnippet 结合使用，可以实现在 vterm 里粘贴 yasnippet 的功能。

eshell：
#+begin_src emacs-lisp
(setq explicit-shell-file-name "/bin/bash")
(setq shell-file-name "/bin/bash")
(setq shell-command-prompt-show-cwd t)
(setq explicit-bash-args '("--noediting" "--login" "-i"))
;; 提示符只读
(setq comint-prompt-read-only t)
;; 命令补全
(setq shell-command-completion-mode t)
;; 高亮模式
(autoload 'ansi-color-for-comint-mode-on "ansi-color" nil t)
(add-hook 'shell-mode-hook 'ansi-color-for-comint-mode-on t)
(setenv "SHELL" shell-file-name)
(setenv "ESHELL" "bash")
(add-hook 'comint-output-filter-functions 'comint-strip-ctrl-m)
#+end_src

* tramp

#+begin_src emacs-lisp
(use-package tramp
  :straight (tramp :files ("lisp/*"))
  :config
  ;; 使用远程主机自己的 PATH(默认是本地的 PATH)
  (add-to-list 'tramp-remote-path 'tramp-own-remote-path)
  ;; 使用 ~/.ssh/config 中的 ssh 持久化配置。（Emacs 默认复用连接，但不持久化连接）
  (setq  tramp-ssh-controlmaster-options nil)
  ;; TRAMP buffers 关闭 version control, 防止卡住。
  (setq vc-ignore-dir-regexp (format "\\(%s\\)\\|\\(%s\\)" vc-ignore-dir-regexp tramp-file-name-regexp))
  ;; 关闭自动保存 ad-hoc proxy 代理配置, 防止为相同 IP 的 VM 配置了错误的 Proxy.
  (setq tramp-save-ad-hoc-proxies nil)
  ;; 调大远程文件名过期时间（默认 10s), 提高查找远程文件性能.
  (setq remote-file-name-inhibit-cache 1800)
  ;;tramp-verbose 10
  (setq tramp-verbose 0)
  ;; 增加压缩传输的文件起始大小（默认 4KB），否则容易出错： “gzip: (stdin): unexpected end of file”
  (setq tramp-inline-compress-start-size (* 1024 8))
  ;; 当文件大小超过 tramp-copy-size-limit 时，用 external methods(如 scp）来传输，从而大大提高拷贝效率。
  (setq tramp-copy-size-limit (* 1024 1024 2))
  (setq tramp-allow-unsafe-temporary-files t)
  ;; 本地不保存 tramp 备份文件。
  (setq tramp-backup-directory-alist `((".*" .  nil)))
  ;; Backup (file~) disabled and auto-save (#file#) locally to prevent delays in editing remote files
  ;; https://stackoverflow.com/a/22077775
  (add-to-list 'backup-directory-alist (cons tramp-file-name-regexp nil))
  ;; 临时目录中保存 TRAMP auto-save 文件, 重启后清空，防止启动时 tramp 扫描文件卡住。
  (setq tramp-auto-save-directory temporary-file-directory)
  ;; 连接历史文件。
  (setq tramp-persistency-file-name (expand-file-name "tramp-connection-history" user-emacs-directory))
  ;; 避免在 shell history 中添加过多 vterm 自动执行的命令。
  (setq tramp-histfile-override nil)
  ;; 在整个 Emacs session 期间保存 SSH 密码.
  (setq password-cache-expiry nil)
  (setq tramp-default-method "ssh")
  (setq tramp-default-remote-shell "/bin/bash")
  (setq tramp-encoding-shell "/bin/bash")
  (setq tramp-default-user "root")
  (setq tramp-terminal-type "tramp")
  (customize-set-variable 'tramp-encoding-shell "/bin/bash")
  (add-to-list 'tramp-connection-properties '("/ssh:" "remote-shell" "/bin/bash"))
  (setq tramp-connection-local-default-shell-variables
        '((shell-file-name . "/bin/bash")
          (shell-command-switch . "-c")))
  
  ;; 自定义远程环境变量。
  (let ((process-environment tramp-remote-process-environment))
    ;; 设置远程环境变量 VTERM_TRAMP, 远程机器的 emacs_bashrc 根据这个变量设置 VTERM 参数。
    (setenv "VTERM_TRAMP" "true")
    (setq tramp-remote-process-environment process-environment)))

;; 切换 Buffer 时设置 VTERM_HOSTNAME 环境变量为多跳的最后一个主机名，并通过 vterm-environment 传递到远程 vterm shell 环境变量中，
;; 这样远程机器 ~/.bashrc 读取并执行的 emacs_bashrc 脚本正确设置 Buffer 名称和 vtem_prompt_end 函数, 从而确保目录跟踪功能正常,
;; 以及通过主机名而非 IP 来打开远程 vterm shell, 确保 SSH ProxyJump 功能正常（只能通过主机名而非 IP 访问），以及避免目标 IP 重复时
;; 连接复用错误的问题。
(defvar my/remote-host "")
(add-hook 'buffer-list-update-hook
          (lambda ()
            (when (file-remote-p default-directory)
              (setq my/remote-host (file-remote-p default-directory 'host))
              ;; 动态计算 ENV=VALUE.
              (require 'vterm)
              (setq vterm-environment `(,(concat "VTERM_HOSTNAME=" my/remote-host))))))

(use-package consult-tramp
  :straight (:repo "Ladicle/consult-tramp" :host github)
  :custom
  ;; 默认为 scpx 模式，不支持 SSH 多跳 Jump。
  (consult-tramp-method "ssh")
  ;; 打开远程的 /root 目录，而非 ~, 避免 tramp hang。
  ;; https://lists.gnu.org/archive/html/bug-gnu-emacs/2007-07/msg00006.html
  (consult-tramp-path "/root/")
  ;; 即使 ~/.ssh/config 正确 Include 了 hosts 文件，这里还是需要配置，因为 consult-tramp 不会解析 Include 配置。
  (consult-tramp-ssh-config "~/work/proxylist/hosts_config"))
#+end_src
+ =tramp-default-method= 缺省值为 scp, 不支持多跳（但拷贝大文件时性能更高），再打开多跳远程文件时每次都需要修改
  /- 中的 -为 ssh，较麻烦，所以设置为 ssh。
+ tramp 打开远程文件时，避免使用 ~ 路径，而应该是绝对路径，[[https://lists.gnu.org/archive/html/bug-gnu-emacs/2007-07/msg00006.html][防止切换 buffer 时卡住]];
+ 修改 net/tramp-sh.el 中的 tramp-send-commad, 将 (concat "exec env TERM='%s' INSIDE_EMACS='%s' " "ENV=%s %s
  PROMPT_COMMAND='' PS1=%s PS2='' PS3='' %s %s") 中最后的 "-i" 去掉， 然后删除同目录下的 tramp-sh.elc 文件；

* others

#+begin_src bash :tangle  ~/.emacs.d/init.sh
which trash || brew install trash
#+end_src

#+begin_src emacs-lisp
;;; Google 翻译
(use-package google-translate
  :straight (:host github :repo "atykhonov/google-translate")
  :config
  (setq max-mini-window-height 0.2)
  ;;(setq google-translate-output-destination 'popup)
  ;;(setq google-translate-output-destination 'kill-ring)
  ;; C-n/p 切换翻译类型。
  (setq google-translate-translation-directions-alist
        '(("en" . "zh-CN") ("zh-CN" . "en")))
  (global-set-key (kbd "C-c d t") #'google-translate-smooth-translate))

;; 增加 imenu 行内容长度。
;;(setq imenu-max-item-length 160)

(setq tab-width 4)
;; 不插入 tab (按照 tab-width 转换为空格插入) 。
(setq-default indent-tabs-mode nil)

;; 保存 Buffer 时自动更新 #+LASTMOD: 时间戳。
(setq time-stamp-start "#\\+\\(LASTMOD\\|lastmod\\):[ \t]*")
(setq time-stamp-end "$")
(setq time-stamp-format "%Y-%m-%dT%02H:%02m:%02S%5z")
;; #+LASTMOD: 必须位于文件开头的 line-limit 行内, 否则自动更新不生效。
(setq time-stamp-line-limit 30)
(add-hook 'before-save-hook 'time-stamp t)

;; 使用 fundamental-mode 打开大文件。
(defun my/large-file-hook ()
  (when (or (string-equal (file-name-extension (buffer-file-name)) "json")
            (string-equal (file-name-extension (buffer-file-name)) "yaml")
            (string-equal (file-name-extension (buffer-file-name)) "yml")
            (string-equal (file-name-extension (buffer-file-name)) "log"))
    (setq buffer-read-only t)
    (font-lock-mode -1)
    (yas-minor-mode -1)
    (smartparens-mode -1)
    (show-smartparens-mode -1)
    (show-paren-mode -1)
    (js2-minor-mode -1)
    ;;(fira-code-mode -1)
    (prettify-symbols-mode -1)
    ;;(symbol-overlay-mode -1)
    (lsp-bridge-mode -1)
    (display-line-numbers-mode -1)
    (highlight-indent-guides-mode -1)
    (visual-fill-column-mode -1)
    (rainbow-delimiters-mode -1)))
(add-hook 'find-file-hook 'my/large-file-hook)

(use-package emacs
  :straight (:type built-in)
  :init
  ;; 粘贴于光标处, 而不是鼠标指针处。
  (setq mouse-yank-at-point t)
  (setq initial-major-mode 'fundamental-mode)
  ;; 按中文折行。
  (setq word-wrap-by-category t)
  ;; 退出自动杀掉进程。
  (setq confirm-kill-processes nil)
  (setq use-short-answers t)
  (setq confirm-kill-emacs #'y-or-n-p)
  (setq ring-bell-function 'ignore)
  ;; 不显示行号, 否则鼠标会飘。
  (add-hook 'artist-mode-hook (lambda () (display-line-numbers-mode -1)))
  ;; bookmark 发生变化时自动保存（默认是 Emacs 正常退出时保存）。
  (setq bookmark-save-flag 1)
  ;; 不创建 lock 文件。
  (setq create-lockfiles nil)
  ;; 启动 Server 。
  (unless (and (fboundp 'server-running-p)
               (server-running-p))
    (server-start)))

(use-package ibuffer
  :straight (:type built-in)
  :config
  (setq ibuffer-expert t)
  (setq ibuffer-use-other-window t)
  (setq ibuffer-movement-cycle nil)
  (setq ibuffer-default-sorting-mode 'recency)
  (setq ibuffer-use-header-line t)
  (add-hook 'ibuffer-mode-hook #'hl-line-mode)
  (global-set-key (kbd "C-x C-b") #'ibuffer))

;; Navigate between buffer and window.
;;(global-set-key (kbd "s-p") 'previous-buffer)
;;(global-set-key (kbd "s-n") 'next-buffer)
(global-set-key (kbd "M-o") 'other-window)

(use-package recentf
  :straight (:type built-in)
  :config
  (setq recentf-save-file "~/.emacs.d/recentf")
  ;; 不自动清理 recentf 记录。
  (setq recentf-auto-cleanup 'never)
  ;; emacs 退出时清理 recentf 记录。
  (add-hook 'kill-emacs-hook #'recentf-cleanup)
  ;; 每 5min 以及 emacs 退出时保存 recentf-list。
  ;;(run-at-time nil (* 5 60) 'recentf-save-list)
  ;;(add-hook 'kill-emacs-hook #'recentf-save-list)
  (setq recentf-max-menu-items 100)
  (setq recentf-max-saved-items 200) ;; default 20
  ;; recentf-exclude 的参数是正则表达式列表，不支持 ~ 引用家目录。
  ;; emacs-dashboard 不显示这里排除的文件。
  
  (setq recentf-exclude `(,(recentf-expand-file-name "~\\(straight\\|ln-cache\\|etc\\|var\\|.cache\\|backup\\|elfeed\\)/.*")
                          ,(recentf-expand-file-name "~\\(recentf\\|bookmarks\\|archived.org\\)")
                          ,tramp-file-name-regexp ;; 不在 recentf 中记录 tramp 文件，防止 tramp 扫描时卡住。
                          "^/tmp" "\\.bak\\'" "\\.gpg\\'" "\\.gz\\'" "\\.tgz\\'" "\\.xz\\'" "\\.zip\\'" "^/ssh:" "\\.png\\'"
                          "\\.jpg\\'" "/\\.git/" "\\.gitignore\\'" "\\.log\\'" "COMMIT_EDITMSG" "\\.pyi\\'" "\\.pyc\\'"
                          "/private/var/.*" "^/usr/local/Cellar/.*" ".*/vendor/.*"
                          ,(concat package-user-dir "/.*-autoloads\\.egl\\'")))
  (recentf-mode +1))

(defvar backup-dir (expand-file-name "~/.emacs.d/backup/"))
(if (not (file-exists-p backup-dir))
    (make-directory backup-dir t))
;; 文件第一次保存时备份。
(setq make-backup-files t)
(setq backup-by-copying t)
;; 不备份 tramp 文件，其它文件都保存到 backup-dir, https://stackoverflow.com/a/22077775
(setq backup-directory-alist `((,tramp-file-name-regexp . nil) (".*" . ,backup-dir)))
;; 备份文件时使用版本号。
(setq version-control t)
;; 删除过多的版本。
(setq delete-old-versions t)
(setq kept-new-versions 6)
(setq kept-old-versions 2)

(defvar autosave-dir (expand-file-name "~/.emacs.d/autosave/"))
(if (not (file-exists-p autosave-dir))
    (make-directory autosave-dir t))
;; auto-save 访问的文件。
(setq auto-save-default t)
(setq auto-save-list-file-prefix autosave-dir)
(setq auto-save-file-name-transforms `((".*" ,autosave-dir t)))
;;(global-auto-revert-mode)

(setq global-mark-ring-max 100)
(setq mark-ring-max 100 )
(setq kill-ring-max 100)

;; minibuffer 历史记录。
(use-package savehist
  :straight (:type built-in)
  :hook (after-init . savehist-mode)
  :config
  (setq history-length 600)
  (setq savehist-save-minibuffer-history t)
  (setq savehist-autosave-interval 200)
  (setq savehist-additional-variables
        '(mark-ring
          global-mark-ring
          extended-command-history)))

;; fill-column 的值应该小于 visual-fill-column-width，否则居中显示时行内容会过长而被隐藏。
(setq-default fill-column 100)
(setq-default comment-fill-column 0)
(setq-default message-log-max t)
(setq-default ad-redefinition-action 'accept)

;; 使用系统剪贴板，实现与其它程序相互粘贴。
(setq x-select-enable-clipboard t)
(setq select-enable-clipboard t)
(setq x-select-enable-primary t)
(setq select-enable-primary t)

;; UTF8 字符。
(prefer-coding-system 'utf-8-unix)
(setenv "LC_ALL" "zh_CN.UTF-8")

;; 删除文件时, 将文件移动到回收站。
(use-package osx-trash
  :config
  (when (eq system-type 'darwin)
    (osx-trash-setup))
  (setq-default delete-by-moving-to-trash t))

;; 在 Finder 中打开当前文件。
(use-package reveal-in-osx-finder :commands (reveal-in-osx-finder))

;; 在帮助文档底部显示 lisp demo.
(use-package elisp-demos
  :config
  (advice-add 'describe-function-1 :after #'elisp-demos-advice-describe-function-1)
  (advice-add 'helpful-update :after #'elisp-demos-advice-helpful-update))

;; 相比 Emacs 内置 Help, 提供更多上下文信息。
(use-package helpful
  :config
  (global-set-key (kbd "C-h f") #'helpful-callable)
  (global-set-key (kbd "C-h v") #'helpful-variable)
  (global-set-key (kbd "C-h k") #'helpful-key)
  (global-set-key (kbd "C-c C-d") #'helpful-at-point)
  (global-set-key (kbd "C-h F") #'helpful-function)
  (global-set-key (kbd "C-h C") #'helpful-command))

;; 在另一个 panel buffer 中展示按键。
(use-package command-log-mode :commands command-log-mode)
(use-package hydra :commands defhydra)

;; macOS 按键调整。
(setq mac-command-modifier 'meta)
;; option 作为 Super 键(按键绑定时： s- 表示 Super，S- 表示 Shift, H- 表示 Hyper)。
(setq mac-option-modifier 'super)
;; fn 作为 Hyper 键。
(setq ns-function-modifier 'hyper)

;; 以下自定义函数参考自：https://github.com/jiacai2050/dotfiles/blob/master/.config/emacs/i-edit.el
(defun my/json-format ()
  (interactive)
  (save-excursion
    (if mark-active
        (json-pretty-print (mark) (point))
      (json-pretty-print-buffer))))

(defun my/delete-file-and-buffer (buffername)
  "Delete the file visited by the buffer named BUFFERNAME."
  (interactive "bDelete file")
  (let* ((buffer (get-buffer buffername))
         (filename (buffer-file-name buffer)))
    (when filename
      (delete-file filename)
      (message "Deleted file %s" filename)
      (kill-buffer))))

(defun my/diff-buffer-with-file ()
  "Compare the current modified buffer with the saved version."
  (interactive)
  (let ((diff-switches "-u")) ;; unified diff
    (diff-buffer-with-file (current-buffer))
    (other-window 1)))

(defun my/copy-current-filename-to-clipboard ()
  "Copy `buffer-file-name' to system clipboard."
  (interactive)
  (let ((filename (if-let (f buffer-file-name)
                      f
                    default-directory)))
    (if filename
        (progn
          (message (format "Copying %s to clipboard..." filename))
          (kill-new filename))
      (message "Not a file..."))))

;; https://gitlab.com/skybert/my-little-friends/-/blob/2022-emacs-from-scratch/emacs/.emacs
;; Rename current buffer, as well as doing the related version control
;; commands to rename the file.
(defun my/rename-this-buffer-and-file ()
  "Renames current buffer and file it is visiting."
  (interactive)
  (let ((filename (buffer-file-name)))
    (if (not (and filename (file-exists-p filename)))
        (message "Buffer is not visiting a file!")
      (let ((new-name (read-file-name "New name: " filename)))
        (cond
         ((vc-backend filename) (vc-rename-file filename new-name))
         (t
          (rename-file filename new-name t)
          (rename-buffer new-name)
          (set-visited-file-name new-name)
          (set-buffer-modified-p nil)
          (message
           "File '%s' successfully renamed to '%s'"
           filename
           (file-name-nondirectory new-name))))))))
(global-set-key (kbd "C-x C-r") 'my/rename-this-buffer-and-file)
#+end_src
+ osx-trash 不支持 TRAMP 删除远程文件，解决办法：用 %m 标记文件，然后按 ! 执行 rm 命令。
+ 参考： [[https://www.masteringemacs.org/article/mastering-key-bindings-emacs][Mastering Key Bindings in Emacs]]

* refs

本配置参考了以下仓库代码：

1. [[https://github.com/seagle0128/.emacs.d][seagle0128/.emacs.d]]
2. [[https://gitlab.com/protesilaos/dotfiles][protesilaos/dotfiles]]
3. [[https://github.com/bbatsov/prelude][bbatsov/prelude]]
4. [[https://github.com/MatthewZMD/.emacs.d][MatthewZMD/.emacs.d]]
5. [[https://github.com/condy0919/.emacs.d][condy0919/.emacs.d]]
6. [[https://github.com/manateelazycat/lazycat-emacs][manateelazycat/lazycat-emacs]]
7. [[https://github.com/jiacai2050/dotfiles][jiacai2050/dotfiles]]
8. [[https://github.com/natecox/dotfiles/][natecox/dotfiles]]
9. [[https://config.daviwil.com/emacs][daviwil]]
10. [[https://gitlab.com/skybert/my-little-friends/-/blob/master/emacs/.emacs][skybert/my-little-friends]]

